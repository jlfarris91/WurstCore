package TimeOfDay
import Math

@configurable public constant real TIME_OF_DAY = 6.0
@configurable public constant real TIME_OF_NIGHT = 18.0

public constant real TIME_OF_DAY_LENGTH = TIME_OF_NIGHT - TIME_OF_DAY
public constant real TIME_OF_NIGHT_LENGTH = (24.0 - TIME_OF_NIGHT) + TIME_OF_DAY

trigger g_OnDayTrigger
trigger g_OnNightTrigger

// ============================================================================
public function getTimeOfDayElapsed() returns real
  let currentTime = GetTimeOfDay()
  if (currentTime >= TIME_OF_DAY and currentTime <= TIME_OF_NIGHT)
    return currentTime - TIME_OF_DAY
  return 0

// ============================================================================
public function getTimeOfNightElapsed() returns real
  let currentTime = GetTimeOfDay()
  if (currentTime >= TIME_OF_NIGHT)
    return currentTime - TIME_OF_NIGHT
  if (currentTime <= TIME_OF_DAY)
    return (24.0 - TIME_OF_NIGHT) + currentTime
  return 0

// ============================================================================
public function isDay() returns bool
  let currentTime = GetTimeOfDay()
  return currentTime >= TIME_OF_DAY and currentTime < TIME_OF_NIGHT

// ============================================================================
public function isNight() returns bool
  let currentTime = GetTimeOfDay()
  return currentTime >= TIME_OF_NIGHT or currentTime < TIME_OF_DAY

// ============================================================================
public function timeOfDayDelta(real current, real target) returns real
  if (current < target)
    return target - current
  return 24.0 - (current - target)

// ============================================================================
public function getTimeOfDayPercentage() returns real
  return clamp01(getTimeOfDayElapsed() / TIME_OF_DAY_LENGTH)

// ============================================================================
public function getTimeOfNightPercentage() returns real
  return clamp01(getTimeOfNightElapsed() / TIME_OF_NIGHT_LENGTH)

// ============================================================================
public function registerDayEvent(code callback)
  g_OnDayTrigger.addCondition(Filter(callback))

// ============================================================================
public function registerNightEvent(code callback)
  g_OnNightTrigger.addCondition(Filter(callback))

// ============================================================================
init

  g_OnDayTrigger = CreateTrigger()
  g_OnDayTrigger.registerGameStateEvent(GAME_STATE_TIME_OF_DAY, EQUAL, TIME_OF_DAY)

  g_OnNightTrigger = CreateTrigger()
  g_OnNightTrigger.registerGameStateEvent(GAME_STATE_TIME_OF_DAY, EQUAL, TIME_OF_NIGHT)