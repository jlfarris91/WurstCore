package IProgress
import Events

// ============================================================================
public interface IProgress
  function getProgress() returns real
  function getMessage() returns string
  function onProgressChanged() returns IEvent1<IProgress>

// ============================================================================
public interface IProgressReporter
  function report(real progress)
  function report(string message)
  function report(real progress, string message)

// ============================================================================
public class Progress implements IProgress, IProgressReporter
  private real m_progress = 0.0
  private string m_message = ""
  private Event1<IProgress> m_progressChangedEvent = null

  // --------------------------------------------------------------------------
  ondestroy
    if (m_progressChangedEvent != null)
      destroy m_progressChangedEvent
      m_progressChangedEvent = null

  // --------------------------------------------------------------------------
  override function getProgress() returns real
    return m_progress

  // --------------------------------------------------------------------------
  override function getMessage() returns string
    return m_message

  // --------------------------------------------------------------------------
  override function report(real progress)
    m_progress = progress
    raiseProgressChangedEvent()

  // --------------------------------------------------------------------------
  override function report(string message)
    m_message = message
    raiseProgressChangedEvent()

  // --------------------------------------------------------------------------
  override function report(real progress, string message)
    m_progress = progress
    m_message = message
    raiseProgressChangedEvent()

  // --------------------------------------------------------------------------
  private function raiseProgressChangedEvent()
    if (m_progressChangedEvent != null)
      m_progressChangedEvent.call(this)

  // --------------------------------------------------------------------------
  override function onProgressChanged() returns IEvent1<IProgress>
    if (m_progressChangedEvent == null)
      m_progressChangedEvent = new Event1<IProgress>()
    return m_progressChangedEvent