package Events
import LinkedList
import ErrorIf
import public Action

// ============================================================================
public class Event
  private LinkedList<EventCallback> m_callbacks

  // --------------------------------------------------------------------------
  construct()
    m_callbacks = new LinkedList<EventCallback>()

  // --------------------------------------------------------------------------
  construct(LinkedList<EventCallback> callbacks)
    m_callbacks = callbacks

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_callbacks
    m_callbacks = null

  // --------------------------------------------------------------------------
  function register(EventCallback callback) returns EventCallback
    ErrorIf.argumentIsNull(callback, "callback")
    callback.e = this
    m_callbacks.add(callback)
    return callback

  // --------------------------------------------------------------------------
  function unregister(EventCallback callback) returns bool
    ErrorIf.argumentIsNull(callback, "callback")
    if (not m_callbacks.remove(callback))
      return false
    callback.e = null
    return true

  // --------------------------------------------------------------------------
  function call()
    for handler in m_callbacks
      if (handler != null)
        handler.call()

// ============================================================================
public class Event1<TArg1>
  private LinkedList<Event1Callback<TArg1>> m_callbacks

  // --------------------------------------------------------------------------
  construct()
    m_callbacks = new LinkedList<Event1Callback<TArg1>>()

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_callbacks
    m_callbacks = null

  // --------------------------------------------------------------------------
  function register(Event1Callback<TArg1> callback) returns Event1Callback<TArg1>
    ErrorIf.argumentIsNull(callback, "callback")
    callback.e = this
    m_callbacks.add(callback)
    return callback

  // --------------------------------------------------------------------------
  function unregister(Event1Callback<TArg1> callback) returns bool
    ErrorIf.argumentIsNull(callback, "callback")
    if (not m_callbacks.remove(callback))
      return false
    callback.e = null
    return true

  // --------------------------------------------------------------------------
  function call(TArg1 arg1)
    for handler in m_callbacks
      if (handler != null)
        handler.call(arg1)

// ============================================================================
public class Event2<TArg1, TArg2>
  private LinkedList<Event2Callback<TArg1, TArg2>> m_callbacks

  // --------------------------------------------------------------------------
  construct()
    m_callbacks = new LinkedList<Event2Callback<TArg1, TArg2>>()

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_callbacks
    m_callbacks = null

  // --------------------------------------------------------------------------
  function register(Event2Callback<TArg1, TArg2> callback) returns Event2Callback<TArg1, TArg2>
    ErrorIf.argumentIsNull(callback, "callback")
    m_callbacks.add(callback)
    callback.e = this
    return callback

  // --------------------------------------------------------------------------
  function unregister(Event2Callback<TArg1, TArg2> callback) returns bool
    ErrorIf.argumentIsNull(callback, "callback")
    if (not m_callbacks.remove(callback))
      return false
    callback.e = null
    return true

  // --------------------------------------------------------------------------
  function call(TArg1 arg1, TArg2 arg2)
    for handler in m_callbacks
      if (handler != null)
        handler.call(arg1, arg2)

// ============================================================================
public class Event3<TArg1, TArg2, TArg3>
  private LinkedList<Event3Callback<TArg1, TArg2, TArg3>> m_callbacks

  // --------------------------------------------------------------------------
  construct()
    m_callbacks = new LinkedList<Event3Callback<TArg1, TArg2, TArg3>>()

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_callbacks
    m_callbacks = null

  // --------------------------------------------------------------------------
  function register(Event3Callback<TArg1, TArg2, TArg3> callback) returns Event3Callback<TArg1, TArg2, TArg3>
    ErrorIf.argumentIsNull(callback, "callback")
    m_callbacks.add(callback)
    callback.e = this
    return callback

  // --------------------------------------------------------------------------
  function unregister(Event3Callback<TArg1, TArg2, TArg3> callback) returns bool
    ErrorIf.argumentIsNull(callback, "callback")
    if (not m_callbacks.remove(callback))
      return false
    callback.e = null
    return true

  // --------------------------------------------------------------------------
  function call(TArg1 arg1, TArg2 arg2, TArg3 arg3)
    for handler in m_callbacks
      if (handler != null)
        handler.call(arg1, arg2, arg3)

// ============================================================================
public abstract class EventCallback
  protected Event e
  abstract function call()
  ondestroy
    if (e != null)
      e.unregister(this)
    e = null

// ============================================================================
public abstract class Event1Callback<TArg1>
  protected Event1<TArg1> e
  abstract function call(TArg1 arg1)
  ondestroy
    if (e != null)
      e.unregister(this)
    e = null

// ============================================================================
public abstract class Event2Callback<TArg1, TArg2>
  protected Event2<TArg1, TArg2> e
  abstract function call(TArg1 arg1, TArg2 arg2)
  ondestroy
    if (e != null)
      e.unregister(this)
    e = null

// ============================================================================
public abstract class Event3Callback<TArg1, TArg2, TArg3>
  protected Event3<TArg1, TArg2, TArg3> e
  abstract function call(TArg1 arg1, TArg2 arg2, TArg3 arg3)
  ondestroy
    if (e != null)
      e.unregister(this)
    e = null