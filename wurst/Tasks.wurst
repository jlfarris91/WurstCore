package Tasks
import LinkedList
import RefObject
import CancellationToken
import ErrorHandling
import Func
import Events
import HashMap
import Action
import Execute
import Object
import ClosureTimers
import CancellableClosureTimers
import Observable

TaskDefinitionBuilder g_taskDefinitionBuilder = new TaskDefinitionBuilder()

// ============================================================================
public class Task
  use Observable

  private TaskDefinition m_definition
  private Task m_parent
  private CancellationToken m_cancellationToken
  private string m_errorMessage = null
  private TaskState m_state = WAITING
  private LinkedList<Action1<Task>> m_actions     // lazily constructed
  private LinkedList<_handle> m_handles           // lazily constructed
  private Event1<Task> m_completed = null         // lazily constructed
  private object m_userData = NULL
  private _handle m_cancellationTokenCanceledHandler = INVALID_HANDLE
  private bool m_destroyOnCompletion = false
  
  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct)
    m_definition = definition
    m_parent = parent
    m_userData = userData
    m_cancellationToken = (ct != null ? ct : new CancellationToken()).acquire()

    m_cancellationTokenCanceledHandler = m_cancellationToken.registerCanceledCallback() -> 
      cancel()

  // --------------------------------------------------------------------------
  ondestroy

    if (m_cancellationTokenCanceledHandler != INVALID_HANDLE)
      m_cancellationToken.unregisterCanceledCallback(m_cancellationTokenCanceledHandler)

    m_cancellationToken.release()
    m_cancellationToken = null

    if (m_handles != null)
      for h in m_handles
        if (m_completed != null)
          m_completed.unregister(h)
      destroy m_handles
      m_handles = null
    
    if (m_completed != null)
      destroy m_completed
      m_completed = null

    if (m_actions != null)
      for action in m_actions
        action.release()
      destroy m_actions
      m_actions = null

  // --------------------------------------------------------------------------
  function getDefinition() returns TaskDefinition
    return m_definition

  // --------------------------------------------------------------------------
  function getUserData<T>() returns T
    return m_userData.castUnsafe<T>()

  // --------------------------------------------------------------------------
  function getUserDataObject() returns object
    return m_userData

  // --------------------------------------------------------------------------
  function getCancellationToken() returns CancellationToken
    return m_cancellationToken

  // --------------------------------------------------------------------------
  function getErrorMessage() returns string
    return m_errorMessage

  // --------------------------------------------------------------------------
  function getState() returns TaskState
    return m_state

  // --------------------------------------------------------------------------
  function completed() returns bool
    return succeeded() or failed() or canceled()

  // --------------------------------------------------------------------------
  function succeeded() returns bool
    return m_state == SUCCEEDED

  // --------------------------------------------------------------------------
  function failed() returns bool
    return m_state == FAILED

  // --------------------------------------------------------------------------
  function canceled() returns bool
    return m_state == CANCELED

  // --------------------------------------------------------------------------
  function running() returns bool
    return m_state == RUNNING

  // --------------------------------------------------------------------------
  function fail(string errorMessage)
    m_state = FAILED
    m_errorMessage = errorMessage
    log("Failed! " + errorMessage)
    complete()
    error(errorMessage)

  // --------------------------------------------------------------------------
  function cancel()
    m_state = CANCELED
    log("Canceled")
    complete()

  // --------------------------------------------------------------------------
  function setDestroyOnCompletion(bool value)
    m_destroyOnCompletion = value

  // --------------------------------------------------------------------------
  function onCompleted(Event1Callback<Task> action) returns _handle

    if (completed())
      action..acquire()..call(this)..release()
      return INVALID_HANDLE

    return registerCompletedEventCallback(action)

  // --------------------------------------------------------------------------
  function onSucceeded(Action1<Task> action) returns _handle

    if (succeeded())
      action..acquire()..call(this)..release()
      return INVALID_HANDLE

    return registerCompletedCallbackAction(action) task ->
      if (task.succeeded())
        action.call(task)

  // --------------------------------------------------------------------------
  function onFailed(Action1<Task> action) returns _handle

    if (failed())
      action..acquire()..call(this)..release()
      return INVALID_HANDLE

    return registerCompletedCallbackAction(action) task ->
      if (task.failed())
        action.call(task)

  // --------------------------------------------------------------------------
  function unregisterOnCompletedEventHandler(_handle callbackHandle)
    if (m_completed != null)
      m_completed.unregister(callbackHandle)

  // --------------------------------------------------------------------------
  private function registerCompletedCallbackAction(Action1<Task> action, Event1Callback<Task> callback) returns _handle
    if (m_actions == null)
      m_actions = new LinkedList<Action1<Task>>()
    m_actions.add(action.acquire())
    return registerCompletedEventCallback(callback)    

  // --------------------------------------------------------------------------
  private function registerCompletedEventCallback(Event1Callback<Task> callback) returns _handle
    if (m_handles == null)
      m_handles = new LinkedList<_handle>()
    if (m_completed == null)
      m_completed = new Event1<Task>()
    let h = m_completed.register(callback)
    m_handles.add(h)
    return h

  // --------------------------------------------------------------------------
  protected function log(string message)
    Log.info(m_definition.getLabel() + ": " + message)

  // --------------------------------------------------------------------------
  protected function run()
    m_state = RUNNING
    // log("Running")

  // --------------------------------------------------------------------------
  protected function succeed()
    m_state = SUCCEEDED
    // log("Succeeded")
    complete()

  // --------------------------------------------------------------------------
  protected function complete()
    // log("Completed")

    if (m_cancellationTokenCanceledHandler != INVALID_HANDLE)
      m_cancellationToken.unregisterCanceledCallback(m_cancellationTokenCanceledHandler)
      m_cancellationTokenCanceledHandler = INVALID_HANDLE

    if (m_completed != null)
      m_completed.call(this)

    if (m_destroyOnCompletion)
      destroy this

  // --------------------------------------------------------------------------
  static function run(Action1<Task> action) returns Task
    return run(null, action)

  // --------------------------------------------------------------------------
  static function run(CancellationToken ct, Action1<Task> action) returns Task
    return run(ct, NULL, action)

  // --------------------------------------------------------------------------
  static function run(CancellationToken ct, object userData, Action1<Task> action) returns Task
    return run(ct, "", userData, action)

  // --------------------------------------------------------------------------
  static function run(CancellationToken ct, string label, object userData, Action1<Task> action) returns Task
    return runOneOffDefinition(new ActionTaskDefinition(null, label, action), ct, userData)

  // --------------------------------------------------------------------------
  static function run<TUserData>(TUserData userData, Action2<Task, TUserData> action) returns Task
    return run<TUserData>(null, "", userData, action)

  // --------------------------------------------------------------------------
  static function run<TUserData>(CancellationToken ct, TUserData userData, Action2<Task, TUserData> action) returns Task
    return run<TUserData>(ct, "", userData, action)

  // --------------------------------------------------------------------------
  static function run<TUserData>(CancellationToken ct, string label, TUserData userData, Action2<Task, TUserData> action) returns Task
    return runOneOffDefinition(new ActionTask2Definition(null, label, action), ct, toObjectUnsafe<TUserData>(userData))

  // --------------------------------------------------------------------------
  static function wait(CancellationToken ct, string label, real seconds) returns Task
    return runOneOffDefinition(new WaitTaskDefinition(null, label, seconds), ct, NULL)

  // --------------------------------------------------------------------------
  static function waitFor(object userData, real pollTime, Func1<Task, bool> predicate) returns Task
    return waitFor("", userData, pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor(string label, object userData, real pollTime, Func1<Task, bool> predicate) returns Task
    return waitFor(null, label, userData, pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor(CancellationToken ct, string label, object userData, real pollTime, Func1<Task, bool> predicate) returns Task
    return runOneOffDefinition(new WaitForTaskDefinition(null, label, pollTime, predicate), ct, userData)

  // --------------------------------------------------------------------------
  static function waitFor<TUserData>(TUserData userData, real pollTime, Func2<Task, TUserData, bool> predicate) returns Task
    return waitFor<TUserData>("", userData, pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor<TUserData>(string label, TUserData userData, real pollTime, Func2<Task, TUserData, bool> predicate) returns Task
    return waitFor<TUserData>(null, label, userData, pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor<TUserData>(CancellationToken ct, string label, TUserData userData, real pollTime, Func2<Task, TUserData, bool> predicate) returns Task
    return runOneOffDefinition(new WaitForTask2Definition<TUserData>(null, label, pollTime, predicate), ct, toObjectUnsafe<TUserData>(userData))

  // --------------------------------------------------------------------------
  static function executeWhile(CancellationToken ct, string label, object userData, real time, int iterations, Func1<Task, bool> action) returns Task
    return runOneOffDefinition(new ExecuteWhileTaskDefinition(null, label, time, iterations, action), ct, userData)

  // --------------------------------------------------------------------------
  static function executeWhile2<TUserData>(CancellationToken ct, string label, TUserData userData, real time, int iterations, Func2<Task, TUserData, bool> action) returns Task
    return runOneOffDefinition(new ExecuteWhileTask2Definition(null, label, time, iterations, action), ct, toObjectUnsafe<TUserData>(userData))

  // --------------------------------------------------------------------------
  static function forEachWhile<T>(CancellationToken ct, string label, object userData, real time, int iterations, Func1<Task, LinkedList<T>> listGetter, Action2<Task, T> action) returns Task
    return runOneOffDefinition(new ForEachWhileTaskDefinition(null, label, time, iterations, listGetter, action), ct, userData)

  // --------------------------------------------------------------------------
  static function forEachWhile2<TUserData, T>(CancellationToken ct, string label, TUserData userData, real time, int iterations, Func2<Task, TUserData, LinkedList<T>> listGetter, Action3<Task, TUserData, T> action) returns Task
    return runOneOffDefinition(new ForEachWhileTask2Definition(null, label, time, iterations, listGetter, action), ct, toObjectUnsafe<TUserData>(userData))

  private static function runOneOffDefinition(TaskDefinition definition, CancellationToken ct, object userData) returns Task
    let task = definition.createTask(null, userData, ct)
    task.onDestroy().register((task) -> destroy definition)
    task.run()
    return task

// ============================================================================
public enum TaskState
  WAITING
  RUNNING
  CANCELED
  FAILED
  SUCCEEDED

// ============================================================================
public function TaskState.toString() returns string
  switch this
    case WAITING
      return "Waiting"
    case RUNNING
      return "Running"
    case CANCELED
      return "Canceled"
    case FAILED
      return "Failed"
    case SUCCEEDED
      return "Succeeded"

// ============================================================================
public abstract class TaskFactory
  use RefObject
  abstract function createTask(TaskDefinition definition, Task parent, object userData, CancellationToken ct) returns Task

// ============================================================================
public abstract class TaskDefinition
  protected TaskDefinition m_parent
  protected string m_label

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label)
    m_parent = parent
    m_label = label

  // --------------------------------------------------------------------------
  function getLabel() returns string
    return m_label

  // --------------------------------------------------------------------------
  function getOwner() returns TaskDefinition
    return m_parent

  // --------------------------------------------------------------------------
  function run() returns Task
    return run(null, NULL)

  // --------------------------------------------------------------------------
  function run(CancellationToken ct) returns Task
    return run(ct, NULL)

  // --------------------------------------------------------------------------
  function run(object userData) returns Task
    return run(null, userData)

  // --------------------------------------------------------------------------
  function run<TUserData>(TUserData userData) returns Task
    return run(null, toObjectUnsafe<TUserData>(userData))

  // --------------------------------------------------------------------------
  function run(CancellationToken ct, object userData) returns Task
    let task = createTask(null, userData, ct)
    task.run()
    return task

  // --------------------------------------------------------------------------
  static function wait(real seconds) returns TaskDefinition
    return wait("", seconds)

  // --------------------------------------------------------------------------
  static function wait(string label, real seconds) returns TaskDefinition
    return new WaitTaskDefinition(null, label, seconds)

  // --------------------------------------------------------------------------
  static function waitFor(real pollTime, Func1<Task, bool> predicate) returns TaskDefinition
    return waitFor("", pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor(string label, real pollTime, Func1<Task, bool> predicate) returns TaskDefinition
    return new WaitForTaskDefinition(null, label, pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor<TUserData>(real pollTime, Func2<Task, TUserData, bool> predicate) returns TaskDefinition
    return waitFor<TUserData>("", pollTime, predicate)

  // --------------------------------------------------------------------------
  static function waitFor<TUserData>(string label, real pollTime, Func2<Task, TUserData, bool> predicate) returns TaskDefinition
    return new WaitForTask2Definition<TUserData>(null, label, pollTime, predicate)

  // --------------------------------------------------------------------------
  static function build(Action1<TaskDefinitionBuilder> taskBuilder) returns TaskDefinition
    return build("", taskBuilder)

  // --------------------------------------------------------------------------
  static function build(string label, Action1<TaskDefinitionBuilder> taskBuilder) returns TaskDefinition
    let definition = new SequenceTaskDefinition(null, label)
    g_taskDefinitionBuilder.reset(definition)
    taskBuilder.acquire()..call(g_taskDefinitionBuilder)..release()
    return definition
    
  // --------------------------------------------------------------------------
  abstract protected function createTask(Task parent, object userData, CancellationToken ct) returns Task

// ============================================================================
abstract class AggregateTaskDefinition extends TaskDefinition
  protected LinkedList<TaskDefinition> m_childDefinitions = new LinkedList<TaskDefinition>()

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label)
    super(parent, label)

  // --------------------------------------------------------------------------
  ondestroy
    while m_childDefinitions.isEmpty() == false
      destroy m_childDefinitions.pop()
    destroy m_childDefinitions
    m_childDefinitions = null
  
  // --------------------------------------------------------------------------
  function getChildDefinitions() returns LinkedList<TaskDefinition>
    return m_childDefinitions

// ============================================================================
class TaskFactoryDefinition extends TaskDefinition
  protected TaskFactory m_factory

  // --------------------------------------------------------------------------
  construct(TaskDefinition owner, string label, TaskFactory factory)
    super(owner, label)
    m_factory = factory.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_factory

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return m_factory.createTask(this, parent, userData, ct)

// ============================================================================
module AggregateTask
  protected LinkedList<Task> m_tasks = new LinkedList<Task>()
  protected HashMap<Task, _handle> m_taskHandles = null

  // --------------------------------------------------------------------------
  ondestroy
    unregisterCompletedCallbacks()
    
    if (m_taskHandles != null)
      destroy m_taskHandles
      m_taskHandles = null

    while not m_tasks.isEmpty()
      destroy m_tasks.pop()
    destroy m_tasks

  // --------------------------------------------------------------------------
  protected function registerCompletedCallback(Task task, Event1Callback<Task> callback)
    if (m_taskHandles == null)
      m_taskHandles = new HashMap<Task, _handle>()
    m_taskHandles.put(task, task.onCompleted(callback))

  // --------------------------------------------------------------------------
  protected function unregisterCompletedCallback(Task task)
    if (m_taskHandles != null)
      task.unregisterOnCompletedEventHandler(m_taskHandles.get(task))
      m_taskHandles.remove(task)

  // --------------------------------------------------------------------------
  protected function unregisterCompletedCallbacks()
    if (m_taskHandles != null and m_taskHandles.size() > 0)
      for task in m_tasks
        task.unregisterOnCompletedEventHandler(m_taskHandles.get(task))
        m_taskHandles.remove(task)

// ============================================================================
public class SequenceTaskDefinition extends AggregateTaskDefinition

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label)
    super(parent, label)
    
  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    let task = new SequenceTask(this, parent, userData, ct)
    for definition in m_childDefinitions
      task.m_tasks.add(definition.createTask(task, userData, ct))
    return task

// ============================================================================
class SequenceTask extends Task
  use AggregateTask

  private LLIterator<Task> m_taskIterator = null
  private Task m_currentTask = null

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct)
    super(definition, parent, userData, ct)

  // --------------------------------------------------------------------------
  ondestroy
    if (m_taskIterator != null)
      m_taskIterator.close()
      m_taskIterator = null

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()

    m_taskIterator = m_tasks.iterator()
    m_taskIterator.reset()

    continue()

  // --------------------------------------------------------------------------
  private function continue()

    if (m_currentTask != null)
    //{
      switch (m_currentTask.getState())
        case SUCCEEDED
          skip
        case FAILED
          fail(m_currentTask.getErrorMessage())
          return
        case CANCELED
          cancel()
          return
        case WAITING | RUNNING
          error("Child task " + m_currentTask.getDefinition().getLabel() + " completed with invalid state " + m_currentTask.getState().toString())
    //}

    let ct = getCancellationToken()

    if (ct.isCanceled())
      cancel()
      return

    if (m_taskIterator.hasNext() == false)
      succeed()
      return

    m_currentTask = m_taskIterator.next()
    registerCompletedCallback(m_currentTask, (task) -> onTaskCompleted(task))

    execute(() -> m_currentTask.run())

  // --------------------------------------------------------------------------
  private function onTaskCompleted(Task task)
    if (task != m_currentTask)
      error("Unexpected task completed")
    unregisterCompletedCallback(task)
    // log("Child task completed " + task.getDefinition().getLabel())
    continue()

// ============================================================================
public class ActionTaskDefinition extends TaskDefinition
  protected Action1<Task> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, Action1<Task> action)
    super(parent, label)
    m_action = action.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()
    
  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new ActionTask(this, parent, userData, ct, m_action)

// ============================================================================
class ActionTask extends Task
  protected Action1<Task> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct, Action1<Task> action)
    super(definition, parent, userData, ct)
    m_action = action.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()

  // --------------------------------------------------------------------------
  override protected function run()
    let ct = getCancellationToken().acquire()

    try() () ->
    //{
      if (ct.isCanceled())
        cancel()
      else
        m_action.call(this)
        succeed()
      ct.release()
    //}

    if (lastError != "")
      fail(lastError)

// ============================================================================
public class ActionTask2Definition<TUserData> extends TaskDefinition
  protected Action2<Task, TUserData> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, Action2<Task, TUserData> action)
    super(parent, label)
    m_action = action.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()
    
  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new ActionTask2<TUserData>(this, parent, userData, ct, m_action)

// ============================================================================
class ActionTask2<TUserData> extends Task
  protected Action2<Task, TUserData> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct, Action2<Task, TUserData> action)
    super(definition, parent, userData, ct)
    m_action = action.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()

  // --------------------------------------------------------------------------
  override protected function run()
    let ct = getCancellationToken().acquire()

    try() () ->
    //{
      if (ct.isCanceled())
        cancel()
      else
        m_action.call(this, getUserData<TUserData>())
        if (not completed())
          succeed()
      ct.release()
    //}

    if (lastError != "")
      fail(lastError)

// ============================================================================
public class WaitTaskDefinition extends TaskDefinition
  private real m_seconds

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real seconds)
    super(parent, (label != null ? label : "Wait({0}s)".format(seconds.toString())))
    m_seconds = seconds
    
  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new WaitTask(this, parent, userData, ct, m_seconds)

// ============================================================================
class WaitTask extends Task
  private real m_seconds
  private CancellableCallbackSingle m_callback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct, real seconds)
    super(definition, parent, userData, ct)
    m_seconds = seconds

  // --------------------------------------------------------------------------
  ondestroy
    destroyCallback()

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()

    m_callback = doAfter(getCancellationToken(), m_seconds) () ->
      // doAfter handles destroying the callback
      m_callback = null
      succeed()

  // --------------------------------------------------------------------------
  override protected function complete()
    destroyCallback()
    super.complete()

  // --------------------------------------------------------------------------
  private function destroyCallback()
    if (m_callback != null)
      m_callback.release()

// ============================================================================
public class WaitForTaskDefinition extends TaskDefinition
  private Func1<Task, bool> m_predicate
  private real m_pollTime

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real pollTime, Func1<Task, bool> predicate)
    super(parent, label)
    m_pollTime = pollTime
    m_predicate = predicate.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_predicate.release()

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new WaitForTask(this, parent, userData, ct, m_pollTime, m_predicate)

// ============================================================================
class WaitForTask extends Task
  private Func1<Task, bool> m_predicate
  private real m_pollTime
  private CancellableCallbackPeriodic m_callback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct, real pollTime, Func1<Task, bool> predicate)
    super(definition, parent, userData, ct)
    m_predicate = predicate.acquire()
    m_pollTime = pollTime

  // --------------------------------------------------------------------------
  ondestroy
    destroyCallback()
    m_predicate.release()

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()

    m_callback = doPeriodically(getCancellationToken(), m_pollTime) cb ->
      if (m_predicate.call(this))
        // doAfter handles destroying the callback
        m_callback = null
        succeed()

  // --------------------------------------------------------------------------
  override protected function complete()
    destroyCallback()
    super.complete()

  // --------------------------------------------------------------------------
  private function destroyCallback()
    if (m_callback != null)
      m_callback.release()

// ============================================================================
public class WaitForTask2Definition<TUserData> extends TaskDefinition
  private Func2<Task, TUserData, bool> m_predicate
  private real m_pollTime

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real pollTime, Func2<Task, TUserData, bool> predicate)
    super(parent, "WaitFor({0} {1}s)".format(label, pollTime.toString()))
    m_pollTime = pollTime
    m_predicate = predicate.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_predicate.release()

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new WaitForTask2(this, parent, userData, ct, m_pollTime, m_predicate)

// ============================================================================
class WaitForTask2<TUserData> extends Task
  private Func2<Task, TUserData, bool> m_predicate
  private real m_pollTime
  private CancellableCallbackPeriodic m_callback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct, real pollTime, Func2<Task, TUserData, bool> predicate)
    super(definition, parent, userData, ct)
    m_predicate = predicate.acquire()
    m_pollTime = pollTime

  // --------------------------------------------------------------------------
  ondestroy
    destroyCallback()
    m_predicate.release()

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()

    m_callback = doPeriodically(getCancellationToken(), m_pollTime) cb ->
      if (m_predicate.call(this, getUserData<TUserData>()))
        nullTimer(() -> succeed())

  // --------------------------------------------------------------------------
  override protected function complete()
    destroyCallback()
    super.complete()

  // --------------------------------------------------------------------------
  private function destroyCallback()
    if (m_callback != null)
      m_callback.release()

// ============================================================================
public class WhenAllTaskDefinition extends AggregateTaskDefinition

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label)
    super(parent, label)

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    let task = new WhenAllTask(this, parent, userData, ct)
    for definition in m_childDefinitions
      task.m_tasks.add(definition.createTask(task, userData, ct))
    return task

// ============================================================================
class WhenAllTask extends Task
  private int m_completed = 0

  use AggregateTask

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct)
    super(definition, parent, userData, ct)

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()
    m_completed = 0
    for task in m_tasks
      registerCompletedCallback(task, (task) -> onTaskCompleted(task))
      execute(() -> task.run())

  // --------------------------------------------------------------------------
  override protected function complete()
    unregisterCompletedCallbacks()
    super.complete()

  // --------------------------------------------------------------------------
  private function onTaskCompleted(Task task)
    unregisterCompletedCallback(task)
    m_completed++
    if (m_completed == m_tasks.size())
      succeed()

// ============================================================================
public class WhenAnyTaskDefinition extends AggregateTaskDefinition

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label)
    super(parent, label)

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    let task = new WhenAnyTask(this, parent, userData, ct)
    for definition in m_childDefinitions
      task.m_tasks.add(definition.createTask(task, userData, ct))
    return task

// ============================================================================
class WhenAnyTask extends Task
  private int m_completed = 0

  use AggregateTask

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct)
    super(definition, parent, userData, ct)

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()
    m_completed = 0
    for task in m_tasks
      registerCompletedCallback(task, (task) -> onTaskCompleted(task))
      execute(() -> task.run())

  // --------------------------------------------------------------------------
  override protected function complete()
    unregisterCompletedCallbacks()
    for task in m_tasks
      if (not task.completed())
        task.cancel()
    super.complete()

  // --------------------------------------------------------------------------
  private function onTaskCompleted(Task task)
    // log("Child task completed: " + task.getDefinition().getLabel() + " " + task.getState().toString())
    unregisterCompletedCallback(task)
    m_completed++
    if (task.succeeded())
      succeed()
    else if (m_completed == m_tasks.size())
      fail("No tasks succeeded")

// ============================================================================
public class ExecuteWhileTaskDefinition extends TaskDefinition
  private int m_iterations
  private real m_time
  private Func1<Task, bool> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real time, int iterations, Func1<Task, bool> action)
    super(parent, label)
    m_time = time
    m_iterations = iterations
    m_action = action.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()  

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new ExecuteWhileTask(this, parent, userData, ct, m_time, m_iterations, m_action)

// ============================================================================
class ExecuteWhileTask extends Task
  private int m_current
  private int m_iterations
  private real m_time
  private Func1<Task, bool> m_action
  private CallbackSingle m_executeOnceCallback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct, real time, int iterations, Func1<Task, bool> action)
    super(definition, parent, userData, ct)
    m_time = time
    m_iterations = iterations
    m_action = action.acquire()
    m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()

    if (m_executeOnceCallback != null)
      destroy m_executeOnceCallback
      m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()
    m_current = 0

    execute(() -> executeOnce())
      
  // --------------------------------------------------------------------------
  private function executeOnce()
    let ct = getCancellationToken()
    var i = 0
    var continue = true

    while (i < m_iterations and continue and not completed() and not ct.isCanceled())
      continue = m_action.call(this)
      i++

    if (completed())
      return

    if (ct.isCanceled())
      cancel()
      return

    if (continue)
      m_executeOnceCallback = doAfter(m_time) () ->
        m_executeOnceCallback = null /* destroyed automatically */
        executeOnce()
    else
      succeed()

// ============================================================================
public class ExecuteWhileTask2Definition<TUserData> extends TaskDefinition
  private int m_iterations
  private real m_time
  private Func2<Task, TUserData, bool> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real time, int iterations, Func2<Task, TUserData, bool> action)
    super(parent, label)
    m_time = time
    m_iterations = iterations
    m_action = action.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()  

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new ExecuteWhileTask2<TUserData>(this, parent, userData, ct, m_time, m_iterations, m_action)

// ============================================================================
class ExecuteWhileTask2<TUserData> extends Task
  private int m_current
  private int m_iterations
  private real m_time
  private Func2<Task, TUserData, bool> m_action
  private CallbackSingle m_executeOnceCallback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct,
            real time, int iterations, Func2<Task, TUserData, bool> action)
    super(definition, parent, userData, ct)
    m_time = time
    m_iterations = iterations
    m_action = action.acquire()
    m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  ondestroy
    m_action.release()

    if (m_executeOnceCallback != null)
      destroy m_executeOnceCallback
      m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()
    m_current = 0

    execute(() -> executeOnce())
      
  // --------------------------------------------------------------------------
  private function executeOnce()
    let ct = getCancellationToken()
    var i = 0
    var continue = true

    while (i < m_iterations and continue and not completed() and not ct.isCanceled())
      continue = m_action.call(this, getUserData<TUserData>())
      i++

    if (completed())
      return

    if (ct.isCanceled())
      cancel()
      return

    if (continue)
      m_executeOnceCallback = doAfter(m_time) () ->
        m_executeOnceCallback = null /* destroyed automatically */
        executeOnce()
    else
      succeed()

// ============================================================================
public class ForEachWhileTaskDefinition<T> extends TaskDefinition
  private int m_iterations
  private real m_time
  private Func1<Task, LinkedList<T>> m_listGetter
  private Action2<Task, T> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real time, int iterations,
            Func1<Task, LinkedList<T>> listGetter, Action2<Task, T> itemAction)
    super(parent, label)
    m_listGetter = listGetter.acquire()
    m_time = time
    m_iterations = iterations
    m_action = itemAction.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_listGetter.release()
    m_action.release()  

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new ForEachWhileTask<T>(this, parent, userData, ct, m_time, m_iterations, m_listGetter, m_action)

// ============================================================================
class ForEachWhileTask<T> extends Task
  private int m_iterations
  private real m_time
  private Func1<Task, LinkedList<T>> m_listGetter
  private Action2<Task, T> m_action
  private LLIterator<T> m_iterator
  private CallbackSingle m_executeOnceCallback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct,
            real time, int iterations, Func1<Task, LinkedList<T>> listGetter, Action2<Task, T> action)
    super(definition, parent, userData, ct)
    m_listGetter = listGetter.acquire()
    m_time = time
    m_iterations = iterations
    m_action = action.acquire()
    m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  ondestroy
    m_listGetter.release()
    m_action.release()

    if (m_iterator != null)
      m_iterator.close()

    if (m_executeOnceCallback != null)
      destroy m_executeOnceCallback
      m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()

    let list = m_listGetter.call(this)

    if (list == null)
      fail("List cannot be null")
      return

    m_iterator = list.iterator()

    execute(() -> executeOnce())
      
  // --------------------------------------------------------------------------
  private function executeOnce()
    let ct = getCancellationToken()
    var i = 0

    while (i < m_iterations and m_iterator.hasNext() and not completed() and not ct.isCanceled())
      m_action.call(this, m_iterator.next())
      i++

    if (completed())
      return

    if (ct.isCanceled())
      cancel()
      return

    if (m_iterator.hasNext())
      m_executeOnceCallback = doAfter(m_time) () ->
        m_executeOnceCallback = null /* destroyed automatically */
        executeOnce()
    else
      succeed()

// ============================================================================
public class ForEachWhileTask2Definition<TUserData, T> extends TaskDefinition
  private int m_iterations
  private real m_time
  private Func2<Task, TUserData, LinkedList<T>> m_listGetter
  private Action3<Task, TUserData, T> m_action

  // --------------------------------------------------------------------------
  construct(TaskDefinition parent, string label, real time, int iterations,
            Func2<Task, TUserData, LinkedList<T>> listGetter, Action3<Task, TUserData, T> itemAction)
    super(parent, label)
    m_listGetter = listGetter.acquire()
    m_time = time
    m_iterations = iterations
    m_action = itemAction.acquire()

  // --------------------------------------------------------------------------
  ondestroy
    m_listGetter.release()
    m_action.release()  

  // --------------------------------------------------------------------------
  override protected function createTask(Task parent, object userData, CancellationToken ct) returns Task
    return new ForEachWhileTask2<TUserData, T>(this, parent, userData, ct, m_time, m_iterations, m_listGetter, m_action)

// ============================================================================
class ForEachWhileTask2<TUserData, T> extends Task
  private int m_iterations
  private real m_time
  private Func2<Task, TUserData, LinkedList<T>> m_listGetter
  private Action3<Task, TUserData, T> m_action
  private LLIterator<T> m_iterator
  private CallbackSingle m_executeOnceCallback

  // --------------------------------------------------------------------------
  construct(TaskDefinition definition, Task parent, object userData, CancellationToken ct,
            real time, int iterations, Func2<Task, TUserData, LinkedList<T>> listGetter, Action3<Task, TUserData, T> action)
    super(definition, parent, userData, ct)
    m_listGetter = listGetter.acquire()
    m_time = time
    m_iterations = iterations
    m_action = action.acquire()
    m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  ondestroy
    m_listGetter.release()
    m_action.release()

    if (m_iterator != null)
      m_iterator.close()

    if (m_executeOnceCallback != null)
      destroy m_executeOnceCallback
      m_executeOnceCallback = null

  // --------------------------------------------------------------------------
  override protected function run()
    super.run()

    let list = m_listGetter.call(this, getUserData<TUserData>())

    if (list == null)
      fail("List cannot be null")
      return

    m_iterator = list.iterator()

    execute(() -> executeOnce())
      
  // --------------------------------------------------------------------------
  private function executeOnce()
    let ct = getCancellationToken()
    var i = 0

    while (i < m_iterations and m_iterator.hasNext() and not completed() and not ct.isCanceled())
      m_action.call(this, getUserData<TUserData>(), m_iterator.next())
      i++

    if (completed())
      return

    if (ct.isCanceled())
      cancel()
      return

    if (m_iterator.hasNext())
      m_executeOnceCallback = doAfter(m_time) () ->
        m_executeOnceCallback = null /* destroyed automatically */
        executeOnce()
    else
      succeed()

// ============================================================================
public class TaskDefinitionBuilder
  private AggregateTaskDefinition m_definition

  // --------------------------------------------------------------------------
  construct ()
    skip

  // --------------------------------------------------------------------------
  construct (AggregateTaskDefinition definition)
    m_definition = definition

  // --------------------------------------------------------------------------
  function reset(AggregateTaskDefinition definition)
    m_definition = definition

  // --------------------------------------------------------------------------
  function execute(Action1<Task> action)
    execute("", action)

  // --------------------------------------------------------------------------
  function execute(string label, Action1<Task> action)
    execute(new ActionTaskDefinition(m_definition, label, action))

  // --------------------------------------------------------------------------
  function execute<TUserData>(Action2<Task, TUserData> action)
    execute("", action)

  // --------------------------------------------------------------------------
  function execute<TUserData>(string label, Action2<Task, TUserData> action)
    execute(new ActionTask2Definition(m_definition, label, action))

  // --------------------------------------------------------------------------
  function execute(TaskDefinition task)
    if (m_definition.m_childDefinitions == null)
      m_definition.m_childDefinitions = new LinkedList<TaskDefinition>()
    m_definition.m_childDefinitions.add(task)

  // --------------------------------------------------------------------------
  function wait(real seconds)
    wait("", seconds)

  // --------------------------------------------------------------------------
  function wait(string label, real seconds)
    execute(new WaitTaskDefinition(null, label, seconds))
  
  // --------------------------------------------------------------------------
  function waitFor(real pollTime, Func1<Task, bool> predicate)
    waitFor("", pollTime, predicate)

  // --------------------------------------------------------------------------
  function waitFor(string label, real pollTime, Func1<Task, bool> predicate)
    execute(new WaitForTaskDefinition(m_definition, label, pollTime, predicate))
  
  // --------------------------------------------------------------------------
  function waitFor<TUserData>(real pollTime, Func2<Task, TUserData, bool> predicate)
    waitFor("", pollTime, predicate)

  // --------------------------------------------------------------------------
  function waitFor<TUserData>(string label, real pollTime, Func2<Task, TUserData, bool> predicate)
    execute(new WaitForTask2Definition(m_definition, label, pollTime, predicate))

  // --------------------------------------------------------------------------
  function whenAll(vararg TaskDefinition taskDefinitions)
    let definition = new WhenAllTaskDefinition(m_definition, "")
    for childDefinition in taskDefinitions
      definition.m_childDefinitions.add(childDefinition)
    execute(definition)

  // --------------------------------------------------------------------------
  function whenAll(string label, LinkedList<TaskDefinition> taskDefinitions, bool ownsTaskDefinitions)
    let definition = new WhenAllTaskDefinition(m_definition, label)
    for childDefinition in taskDefinitions
      definition.m_childDefinitions.add(childDefinition)
    execute(definition)
    if (ownsTaskDefinitions)
      destroy taskDefinitions

  // --------------------------------------------------------------------------
  function whenAny(vararg TaskDefinition taskDefinitions)
    let definition = new WhenAnyTaskDefinition(m_definition, "")
    for childDefinition in taskDefinitions
      definition.m_childDefinitions.add(childDefinition)
    execute(definition)

  // --------------------------------------------------------------------------
  function whenAny(string label, LinkedList<TaskDefinition> taskDefinitions, bool ownsTaskDefinitions)
    let definition = new WhenAnyTaskDefinition(m_definition, label)
    for childDefinition in taskDefinitions
      definition.m_childDefinitions.add(childDefinition)
    execute(definition)
    if (ownsTaskDefinitions)
      destroy taskDefinitions

  // --------------------------------------------------------------------------
  function executeWhile(string label, real time, int iterations, Func1<Task, bool> action)
    execute(new ExecuteWhileTaskDefinition(m_definition, label, time, iterations, action))

  // --------------------------------------------------------------------------
  function executeWhile2<TUserData>(string label, real time, int iterations, Func2<Task, TUserData, bool> action)
    execute(new ExecuteWhileTask2Definition(m_definition, label, time, iterations, action))

  // --------------------------------------------------------------------------
  function forEachWhile<T>(string label, real time, int iterations, Func1<Task, LinkedList<T>> listGetter, Action2<Task, T> action)
    execute(new ForEachWhileTaskDefinition(m_definition, label, time, iterations, listGetter, action))

  // --------------------------------------------------------------------------
  function forEachWhile2<TUserData, T>(string label, real time, int iterations, Func2<Task, TUserData, LinkedList<T>> listGetter, Action3<Task, TUserData, T> action)
    execute(new ForEachWhileTask2Definition(m_definition, label, time, iterations, listGetter, action))