package Loaded
import RegisterEvents
import HashMap
import Orders
import ErrorHandling

HashMap<unit, unit> g_loadedUnits = new HashMap<unit, unit>()
trigger g_unitUnloadedTrigger = CreateTrigger()
unit array g_unloadedUnitStack
int g_unloadedUnitStackCount

// ============================================================================
/** Register a callback for the event of a unit being unloaded.
    Use getUnloadedUnit() to get the unloaded unit.
    Note: isUnitLoaded() will return true for the unloaded unit.
*/
public function registerUnitUnloadedEvent(code callback)
  g_unitUnloadedTrigger.addCondition(Condition(callback))

// ============================================================================
/** Gets the unit being unloaded for the unit unloaded event */
public function getUnloadedUnit() returns unit
  return peekUnloadedUnit()

// ============================================================================
/** Gets a value indicating whether the given unit is being held by another */
public function isUnitLoaded(unit _unit) returns bool
  return g_loadedUnits.has(_unit)

// ============================================================================
/** Gets the unit that is holding the loaded unit */
public function getTransportUnit(unit loadedUnit) returns unit
  return g_loadedUnits.get(loadedUnit)

// ============================================================================
function pushUnloadedUnit(unit unloadedUnit)
  g_unloadedUnitStack[g_unloadedUnitStackCount] = unloadedUnit
  g_unloadedUnitStackCount++

// ============================================================================
function peekUnloadedUnit() returns unit
  return g_unloadedUnitStackCount > 0 ? g_unloadedUnitStack[g_unloadedUnitStackCount-1] : null

// ============================================================================
function popUnloadedUnit() returns unit
  if (g_unloadedUnitStackCount == 0)
    error("Attempting to pop the unloaded unit stack even though it is empty")
  let result = g_unloadedUnitStack[g_unloadedUnitStackCount]
  g_unloadedUnitStack[g_unloadedUnitStackCount] = null
  g_unloadedUnitStackCount--
  return result

// ============================================================================
function raiseUnitUnloadedEvent(unit _unit)
  Log.debug("Unit " + _unit.getName() + " unloaded from unit " + getTransportUnit(_unit).getName())
  pushUnloadedUnit(_unit)
  g_unitUnloadedTrigger.evaluate()
  popUnloadedUnit()

// ============================================================================
function onUnitLoaded()
  let loadedUnit = GetLoadedUnit()
  let loadingUnit = GetTransportUnit()
  g_loadedUnits.put(loadedUnit, loadingUnit)
  Log.debug("Unit " + loadedUnit.getName() + " loaded into unit " + loadingUnit.getName())

// ============================================================================
function onUnitIssuedOrder()
  let orderedUnit = GetOrderedUnit()
  if (GetIssuedOrderId() == OrderIds.stop and g_loadedUnits.has(orderedUnit))
    raiseUnitUnloadedEvent(orderedUnit)
    g_loadedUnits.remove(orderedUnit)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_LOADED, function onUnitLoaded)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function onUnitIssuedOrder)