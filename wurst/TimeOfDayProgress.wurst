package TimeOfDayProgress
import TimeOfDay
import IProgress
import Events
import RealtimeUpdate
import Math
import Time

public TimeOfDayProgress g_dayProgress
public TimeOfDayProgress g_nightProgress

// ============================================================================
public class TimeOfDayProgress implements IProgress, IRealtimeListener
  private Event1<IProgress> m_progressChangedEvent
  private time m_timeOfDayOnStart
  private timespan m_duration
  private time m_startTime

  // --------------------------------------------------------------------------
  ondestroy
    this.unregisterForRealtimeUpdate()

    if (m_progressChangedEvent != null)
      destroy m_progressChangedEvent
      m_progressChangedEvent = null

  // --------------------------------------------------------------------------
  function getDuration() returns timespan
    return m_duration

  // --------------------------------------------------------------------------
  function setDuration(timespan duration)
    m_duration = duration

  // --------------------------------------------------------------------------
  function getStartTime() returns time
    return m_timeOfDayOnStart

  // --------------------------------------------------------------------------
  function setStartTime(time timeOfDayOnStart)
    m_timeOfDayOnStart = timeOfDayOnStart

  // --------------------------------------------------------------------------
  function getElapsed() returns timespan
    return getGameTime() - m_startTime

  // --------------------------------------------------------------------------
  function start()
    m_startTime = m_timeOfDayOnStart
    m_startTime = makeTime(getDay(), m_startTime.getHour(), m_startTime.getMinute())
    
    raiseProgressChangedEvent()
    this.registerForRealtimeUpdate(RealtimeUpdatePriority.Slow)

  // --------------------------------------------------------------------------
  function complete()
    raiseProgressChangedEvent()
    this.unregisterForRealtimeUpdate()

  // --------------------------------------------------------------------------
  override function getProgress() returns real
    return clamp01(getElapsed() / m_duration)

  // --------------------------------------------------------------------------
  override function getProgressChangedEvent() returns Event1<IProgress>
    if (m_progressChangedEvent == null)
      m_progressChangedEvent = new Event1<IProgress>()
    return m_progressChangedEvent

  // --------------------------------------------------------------------------
  override function realtimeUpdate(real dt)
    raiseProgressChangedEvent()

  // --------------------------------------------------------------------------
  function raiseProgressChangedEvent()
    if (m_progressChangedEvent != null)
      m_progressChangedEvent.call(this)

// ============================================================================
function onDayStart()
  g_nightProgress.complete()
  g_dayProgress.start()

// ============================================================================
function onNightStart()
  g_dayProgress.complete()
  g_nightProgress.start()

// ============================================================================
init
  g_dayProgress = new TimeOfDayProgress()
    ..setStartTime(DAY_TIME)
    ..setDuration(DAY_LENGTH)

  g_nightProgress = new TimeOfDayProgress()
    ..setStartTime(NIGHT_TIME)
    ..setDuration(NIGHT_LENGTH)

  registerDayEvent(function onDayStart)
  registerNightEvent(function onNightStart)