package PanelLite
import FrameLite

// ============================================================================
public class PanelLite extends FrameLite
  private IFrameLiteLayout m_layout
  private bool m_layingOut

  // --------------------------------------------------------------------------
  construct()
    super(createFrame("FRAME", "Panel", GAME_UI, "", 0))

  // --------------------------------------------------------------------------
  ondestroy
    if (m_layout != null)
      destroy m_layout
      m_layout = null
  
  // --------------------------------------------------------------------------
  override function isPanel() returns bool
    return true

  // --------------------------------------------------------------------------
  function getFrameLayout() returns IFrameLiteLayout
    return m_layout

  // --------------------------------------------------------------------------
  protected function setFrameLayout(IFrameLiteLayout value)
    m_layout = value

  // --------------------------------------------------------------------------
  override function layout()
    if (m_layingOut)
      return
    m_layingOut = true
    super.layout()
    if (m_layout != null)
      m_layout.layout(this)
    else
      let children = getChildren()
      let padding = getPadding()
      for child in children
        FrameLite.layoutFrame(this, child, padding)
    m_layingOut = false