package Dispatcher
import Action
import Events
import RefObject
import ErrorHandling
import ClosureTimers

DispatcherContext array g_playerDispatchers

// ============================================================================
public interface IDispatcher
  function getDispatcherContext(bool requireEnabled) returns DispatcherContext

// ============================================================================
public abstract class DispatcherContext
  use RefObject
  abstract function canDispatch() returns bool

// ============================================================================
public function IDispatcher.dispatch(Action work)
  this.dispatch(true, work)

// ============================================================================
public function IDispatcher.dispatch(bool requireEnabled, Action work)
  dispatch_Internal(this.getDispatcherContext(requireEnabled), work)

// ============================================================================
public function IDispatcher.dispatchAfter(real seconds, Action work) returns CallbackSingle
  return this.dispatchAfter(seconds, true, work)

// ============================================================================
public function IDispatcher.dispatchAfter(real seconds, bool requireEnabled, Action work) returns CallbackSingle
  return doAfter(seconds, new DispatchedCallbackSingle(this.getDispatcherContext(requireEnabled), work))

// ============================================================================
public function IEvent.registerDispatched(IDispatcher dispatcher, Action handler) returns _handle
  return this.registerDispatched(dispatcher, true, handler)

// ============================================================================
public function IEvent.registerDispatched(IDispatcher dispatcher, bool requireEnabled, Action handler) returns _handle
  return this.register(new DispatchedEventCallback(dispatcher.getDispatcherContext(requireEnabled), handler))

// ============================================================================
public function IEvent1<T>.registerDispatched<T>(IDispatcher dispatcher, bool requireEnabled, Action1<T> handler) returns _handle
  return this.register(new DispatchedEvent1Callback<T>(dispatcher.getDispatcherContext(requireEnabled), handler))

// ============================================================================
function dispatch_Internal(DispatcherContext context, Action work)
  context.acquire()
  work.acquire()
  if (context.canDispatch())
    work.acquire()
  work.release()
  context.release()

// ============================================================================
function dispatch_Internal<T>(DispatcherContext context, Action1<T> work, T arg1)
  context.acquire()
  work.acquire()
  if (context.canDispatch())
    work.call(arg1)
  work.release()
  context.release()

// ============================================================================
module DispatchedWorkModule
  protected DispatcherContext m_context
  protected Action m_work

  // --------------------------------------------------------------------------
  protected function _construct(DispatcherContext context, Action work)
    if (context == null)
      argumentNullError("context")
    if (work == null)
      argumentNullError("work")
    m_context = context.acquire()
    m_work = work.acquire()
  
  // --------------------------------------------------------------------------
  ondestroy
    m_work.release()
    m_context.release()

// ============================================================================
class DispatchedCallbackSingle extends CallbackSingle
  use DispatchedWorkModule

  // --------------------------------------------------------------------------
  construct(DispatcherContext context, Action work)
    _construct(context, work)

  // --------------------------------------------------------------------------
  override function call()
    dispatch_Internal(m_context, m_work)

// ============================================================================
class DispatchedEventCallback extends EventCallback
  use DispatchedWorkModule

  // --------------------------------------------------------------------------
  construct(DispatcherContext context, Action work)
    _construct(context, work)

  // --------------------------------------------------------------------------
  override function call()
    dispatch_Internal(m_context, m_work)

// ============================================================================
module DispatchedWork1Module<T>
  protected DispatcherContext m_context
  protected Action1<T> m_work

  // --------------------------------------------------------------------------
  protected function _construct(DispatcherContext context, Action1<T> work)
    if (context == null)
      argumentNullError("context")
    if (work == null)
      argumentNullError("work")
    m_context = context.acquire()
    m_work = work.acquire()
  
  // --------------------------------------------------------------------------
  ondestroy
    m_work.release()
    m_context.release()

// ============================================================================
class DispatchedEvent1Callback<T> extends Event1Callback<T>
  use DispatchedWork1Module<T>

  // --------------------------------------------------------------------------
  construct(DispatcherContext context, Action1<T> work)
    _construct(context, work)

  // --------------------------------------------------------------------------
  override function call(T arg1)
    dispatch_Internal(m_context, m_work, arg1)

// ============================================================================
public class InGamePlayerDispatcher extends DispatcherContext
  private player m_ownerPlayer

  // --------------------------------------------------------------------------
  construct(player ownerPlayer)
    m_ownerPlayer = ownerPlayer

  // --------------------------------------------------------------------------
  override function canDispatch() returns bool
    return m_ownerPlayer != null and m_ownerPlayer.isIngame()

// ============================================================================
public function player.getDispatcherContext() returns DispatcherContext
  if (g_playerDispatchers[this.getId()] == null)
    g_playerDispatchers[this.getId()] = new InGamePlayerDispatcher(this)
  return g_playerDispatchers[this.getId()]