package Deck
import WeightedSet
import HashMap
import ErrorHandling
import SmartValue
import RefObject

// ============================================================================
public class Card
  private string m_id
  private SmartValue m_cost
  private real m_weight = 1.0

  use RefObject

  // --------------------------------------------------------------------------
  construct(string id)
    m_id = id
    m_cost = new SmartValue()

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_cost
    m_cost = null

  // --------------------------------------------------------------------------
  function getId() returns string
    return m_id

  // --------------------------------------------------------------------------
  function getCost() returns int
    return m_cost.getIntValue()

  // --------------------------------------------------------------------------
  function getSmartCost() returns SmartValue
    return m_cost
  
  // --------------------------------------------------------------------------
  function setCost(int value)
    m_cost.setBase(value)

  // ----------------------------------------------------------------------------
  function getWeight() returns real
    return m_weight
  
  // ----------------------------------------------------------------------------
  function setWeight(real value)
    m_weight = value

  // --------------------------------------------------------------------------
  function canActivate() returns bool
    return true

// ============================================================================
public class CardCategory
  private Deck m_deck
  private string m_id
  private WeightedSet<Card> m_cards = new WeightedSet<Card>()
  private Card m_leastExpensiveCard = null
  private Card m_mostExpensiveCard = null

  // --------------------------------------------------------------------------
  construct(Deck deck, string id)
    m_deck = deck
    m_id = id

  // --------------------------------------------------------------------------
  function getId() returns string
    return m_id

  // --------------------------------------------------------------------------
  function getCards() returns WeightedSet<Card>
    return m_cards

  // --------------------------------------------------------------------------
  function addCard(Card card)

    card.acquire()

    m_cards.add(card, card.getWeight())

    if (m_leastExpensiveCard == null or card.getCost() < m_leastExpensiveCard.getCost())
      m_leastExpensiveCard = card
      m_deck.updateLeastExpensiveCard()

    if (m_mostExpensiveCard == null or card.getCost() > m_mostExpensiveCard.getCost())
      m_mostExpensiveCard = card
      m_deck.updateMostExpensiveCard()

  // --------------------------------------------------------------------------
  function removeCard(Card card)
    m_cards.remove(card)

    if (m_leastExpensiveCard == card)
      updateLeastExpensiveCard()

    if (m_mostExpensiveCard == card)
      updateMostExpensiveCard()

    card.release()

  // --------------------------------------------------------------------------
  function getCardWeight(Card card) returns real
    return m_cards.getWeight(card)

  // --------------------------------------------------------------------------
  function getCardRelativeWeight(Card card) returns real
    return m_cards.getChance(card)

  // --------------------------------------------------------------------------
  function drawCard() returns Card
    return m_cards.getRandom()

  // --------------------------------------------------------------------------
  function popCard() returns Card
    return m_cards.popRandom()

  // --------------------------------------------------------------------------
  function getLeastExpensiveCard() returns Card
    return m_leastExpensiveCard

  // --------------------------------------------------------------------------
  function getMostExpensiveCard() returns Card
    return m_mostExpensiveCard

  // --------------------------------------------------------------------------
  private function updateLeastExpensiveCard()
    m_leastExpensiveCard = null
    for otherCard in m_cards
      if (m_leastExpensiveCard == null or otherCard.getCost() < m_leastExpensiveCard.getCost())
        m_leastExpensiveCard = otherCard
    m_deck.updateLeastExpensiveCard()

  // --------------------------------------------------------------------------
  private function updateMostExpensiveCard()
    m_mostExpensiveCard = null
    for otherCard in m_cards
      if (m_mostExpensiveCard == null or otherCard.getCost() > m_mostExpensiveCard.getCost())
        m_mostExpensiveCard = otherCard
    m_deck.updateMostExpensiveCard()

// ============================================================================
public class Deck
  private HashMap<string, CardCategory> m_categoryMap = new HashMap<string, CardCategory>()
  private WeightedSet<CardCategory> m_categories = new WeightedSet<CardCategory>()
  private Card m_leastExpensiveCard = null
  private Card m_mostExpensiveCard = null

  use RefObject

  // --------------------------------------------------------------------------
  function addDefaultCategory() returns CardCategory
    return addDefaultCategory(1.0)

  // --------------------------------------------------------------------------
  function addDefaultCategory(real weight) returns CardCategory
    return addCategory("default", weight)

  // --------------------------------------------------------------------------
  function addCategory(string id, real weight) returns CardCategory

    if (m_categoryMap.has(id))
      error("Deck already has a category with id " + id)
      return null

    let category = new CardCategory(this, id)
    m_categories.add(category, weight)
    m_categoryMap.put(id, category)
    return category

  // --------------------------------------------------------------------------
  function removeCategory(string id) returns bool
    let category = getCategory(id)
    if (category == null)
      return false
    m_categories.remove(category)
    m_categoryMap.remove(category.getId())
    return true

  // --------------------------------------------------------------------------
  function getCategories() returns WeightedSet<CardCategory>
    return m_categories

  // --------------------------------------------------------------------------
  function getCategory(string id) returns CardCategory
    return m_categoryMap.get(id)

  // ----------------------------------------------------------------------------
  function getCardRelativeWeight(CardCategory category, Card card) returns real
    let categoryRelativeWeight = m_categories.getChance(category)
    let cardRelativeWeight = category.getCardRelativeWeight(card)
    return categoryRelativeWeight * cardRelativeWeight

  // ----------------------------------------------------------------------------
  function getLeastExpensiveCard() returns Card
    return m_leastExpensiveCard

  // --------------------------------------------------------------------------
  function getMostExpensiveCard() returns Card
    return m_mostExpensiveCard

  // --------------------------------------------------------------------------
  function drawCard() returns Card
    let category = m_categories.getRandom()
    return category.drawCard()

  // ----------------------------------------------------------------------------
  function createInstance() returns DeckInstance
    return new DeckInstance(this)

  // --------------------------------------------------------------------------
  protected function updateLeastExpensiveCard()
    m_leastExpensiveCard = null
    for category in m_categories
      let categoryCard = category.getLeastExpensiveCard()
      if (categoryCard != null)
        if (m_leastExpensiveCard == null or categoryCard.getCost() < m_leastExpensiveCard.getCost())
          m_leastExpensiveCard = categoryCard

  // --------------------------------------------------------------------------
  protected function updateMostExpensiveCard()
    m_mostExpensiveCard = null
    for category in m_categories
      let categoryCard = category.getMostExpensiveCard()
      if (categoryCard != null)
        if (m_mostExpensiveCard == null or categoryCard.getCost() > m_mostExpensiveCard.getCost())
          m_mostExpensiveCard = categoryCard

// ============================================================================
public class DeckInstance
  private Deck m_deck
  private WeightedSet<Card> m_cards

  // ----------------------------------------------------------------------------
  construct(Deck deck)
    m_deck = deck.acquire()
    m_cards = new WeightedSet<Card>()

    let categories = m_deck.getCategories()
    for cat in categories
      for card in cat.getCards()
        let weight = m_deck.getCardRelativeWeight(cat, card)
        m_cards.add(card, weight)

  // ----------------------------------------------------------------------------
  ondestroy
    destroy m_cards
    m_cards = null

    m_deck.release()
    m_deck = null

  // ----------------------------------------------------------------------------
  function draw() returns Card
    return m_cards.popRandom()

  // ----------------------------------------------------------------------------
  function add(Card card)
    m_cards.add(card, card.getWeight())

  // ----------------------------------------------------------------------------
  function remove(Card card) returns bool
    return m_cards.remove(card)
  
  // ----------------------------------------------------------------------------
  function isEmpty() returns bool
    return m_cards.isEmpty()