package Missile
import ECS
import RealtimeUpdate
import Math
import Events
import Func
import Action
import Quaternion
import GroupUtils

// Debug:
// import ClosureTimers

constant real DEFAULT_ARRIVAL_THRESHOLD = 10.0
constant group g_tempGroup = CreateGroup()

// ============================================================================
public class ECMissileEffect extends ECComponent
  protected static ECMissileEffect array g_components

  private effect m_effect = null

  bool m_updateOrientation = true

  // --------------------------------------------------------------------------
  construct(ECEntity entity)
    super(entity)
    g_components[entity.getId()] = this

  // --------------------------------------------------------------------------
  ondestroy
    g_components[getEntity().getId()] = null

    if (m_effect != null)
      m_effect.destr()
      m_effect = null

  // ----------------------------------------------------------------------------
  function setEffect(effect value)
    if (m_effect != null)
      m_effect.destr()
    m_effect = value

  // ----------------------------------------------------------------------------
  function getEffect() returns effect
    return m_effect

// ============================================================================
public class ECMissile extends ECComponent
  protected static ECMissile array g_components

  unit m_owner = null
  unit m_targetUnit = null    // Homing

  real m_time = 0.0
  real m_progress = 0.0
  real m_lastProgress = 0.0

  vec2 m_startPos = ZERO2
  vec2 m_targetPos = ZERO2

  angle m_projectileLaunchYaw = angle(0)
  vec3  m_projectileLaunchOffset = ZERO3
  real  m_projectileLaunchFlyHeight = 0.0

  angle m_projectileImpactYaw = angle(0)
  vec3  m_projectileImpactOffset = ZERO3
  real  m_projectileImpactFlyHeight = 0.0

  real m_speed = 0.0 
  real m_arc = 0.0
  real m_arrivalThreshold = DEFAULT_ARRIVAL_THRESHOLD
  vec3 m_arcUp = UP3

  private Event1<ECMissile> m_onArrived = null

  // --------------------------------------------------------------------------
  construct(ECEntity entity)
    super(entity)
    g_components[entity.getId()] = this

  // --------------------------------------------------------------------------
  ondestroy
    g_components[getEntity().getId()] = null

    if (m_onArrived != null)
      destroy m_onArrived
      m_onArrived = null

  // ----------------------------------------------------------------------------
  function onArrived() returns IEvent1<ECMissile>
    if (m_onArrived == null)
      m_onArrived = new Event1<ECMissile>()
    return m_onArrived

  // ----------------------------------------------------------------------------
  function arrived()
    m_progress = 1.0
    if (m_onArrived != null)
      m_onArrived.call(this)

  // ----------------------------------------------------------------------------
  function setFiringUnit(unit value)
    m_owner = value
    m_startPos = value.getPos()
    m_projectileLaunchYaw = value.getFacingAngle()
    m_projectileLaunchFlyHeight = value.getFlyHeight()

  // ----------------------------------------------------------------------------
  /** Will cause desync! */
  function getProjectileLaunchPos() returns vec3
    var targetPos = getStartPos()
    let offsetZ = m_projectileLaunchOffset.z + m_projectileLaunchFlyHeight
    if (m_projectileLaunchYaw != angle(0))
      targetPos += getOffsetRotated(m_projectileLaunchOffset, m_projectileLaunchYaw.radians())
    return targetPos.withTerrainZ(offsetZ)

  // ----------------------------------------------------------------------------
  /** Will cause desync! */
  function getProjectileImpactPos() returns vec3
    var targetPos = getTargetPos()
    var offsetZ = m_projectileImpactOffset.z + m_projectileImpactFlyHeight
    if (m_targetUnit != null)
    //{
      targetPos += getOffsetRotated(m_projectileImpactOffset, m_targetUnit.getFacingAngle().radians())
      offsetZ += m_targetUnit.getFlyHeight()
    //}
    else if (m_projectileImpactYaw != angle(0))
    //{
      targetPos += getOffsetRotated(m_projectileImpactOffset, m_projectileImpactYaw.radians())
    //}
    return targetPos.withTerrainZ(offsetZ)

  // ----------------------------------------------------------------------------
  function getStartPos() returns vec2
    return m_startPos

  // ----------------------------------------------------------------------------
  function getTargetPos() returns vec2
    return m_targetUnit != null ? m_targetUnit.getPos() : m_targetPos

  // ----------------------------------------------------------------------------
  private function getOffsetRotated(vec3 offset, real yaw) returns vec2
    if (offset.x != 0.0 or offset.y != 0.0)
      let rot = eulerToQuat(yaw, 0.0, 0.0)
      return (rot * offset).toVec2()
    return offset.toVec2()

// ============================================================================
public class MissileMovementSystem extends ECSystem1<ECMissile>

  // --------------------------------------------------------------------------
  construct()
    super(ECMissile.typeId)

  // --------------------------------------------------------------------------
  override protected function updateEntity(ECEntity entity, ECMissile missile, real dt)

    if (entity.isMarkedForDestroy())
      return

    missile.m_time += dt

    let startPos2 = missile.getStartPos()
    let endPos2 = missile.getTargetPos()

    let startToEnd2 = endPos2 - startPos2
    let totalDistance2 = startToEnd2.length()
    let distTraveled = missile.m_speed * missile.m_time
    let t = totalDistance2 == 0.0 ? 1.0 : clamp01(distTraveled / totalDistance2)
    
    missile.m_lastProgress = missile.m_progress
    missile.m_progress = t

    // Either way the missile has already arrived
    if (t == 1.0)
    //{
      entity.markForDestroy()
      missile.arrived()
      return
    //}

// ============================================================================
public class MissileEffectSystem extends ECSystem2<ECMissile, ECMissileEffect>

  // --------------------------------------------------------------------------
  construct()
    super(ECMissile.typeId, ECMissileEffect.typeId)

  // --------------------------------------------------------------------------
  override protected function updateEntity(ECEntity entity, ECMissile missile, ECMissileEffect missileEffect, real dt)
    
    let fx = missileEffect.getEffect()
    if (fx == null)
      return

    if (entity.isMarkedForDestroy())
      return

    // Careful! Everything below should only be local since it uses GetTerrainZ()
    let t = missile.m_progress
    let up = missile.m_arcUp
    let startPos = missile.getProjectileLaunchPos()
    let endPos = missile.getProjectileImpactPos()
    let startToEnd = endPos - startPos
    let totalDistance = startToEnd.length()
    let arcRads = t * PI
    let arcZOffset = Sin(arcRads) * missile.m_arc * totalDistance
    let effectPos = lerp(startPos, endPos, t) + up * arcZOffset

    fx.setPos(effectPos)

    if (not missileEffect.m_updateOrientation)
      return

    let rightDir = startToEnd.norm()
    let arcVel = vec3(1, 0, PI * Cos(arcRads) * missile.m_arc).norm()
    let effectRotFinal = quatLookRotation(rightDir, up) * quatLookRotation(arcVel, up)

    fx.setOrientation(effectRotFinal)

// ============================================================================
public class ECMissileAreaDamage extends ECComponent
  protected static ECMissileAreaDamage array g_components

  private Action1<unit> m_damageCallback = null
  private Func1<unit, bool> m_filter = null

  real m_startSize = 0
  real m_endSize = 0
  real m_damage = 0
  bool m_ignoreMissileOwner = true
  bool m_ignoreMissileTarget = false
  group m_affectedUnits = getGroup()

  vec2 m_lastPos = ZERO2
  real m_lastSize = 0.0

  bool m_initialized = false

  // --------------------------------------------------------------------------
  construct(ECEntity entity)
    super(entity)
    g_components[entity.getId()] = this

  // --------------------------------------------------------------------------
  ondestroy
    g_components[getEntity().getId()] = null
    
    if (m_affectedUnits != null)
      m_affectedUnits.release()
      m_affectedUnits = null

    if (m_damageCallback != null)
      m_damageCallback.release()
      m_damageCallback = null
    
    if (m_filter != null)
      m_filter.release()
      m_filter = null

  // ----------------------------------------------------------------------------
  function getTargetFilter() returns Func1<unit, bool>
    return m_filter
  
  // ----------------------------------------------------------------------------
  function setTargetFilter(Func1<unit, bool> value)
    value.acquire()
    if (m_filter != null)
      m_filter.release()
    m_filter = value

  // ----------------------------------------------------------------------------
  function getDamageCallback() returns Action1<unit>
    return m_damageCallback
  
  // ----------------------------------------------------------------------------
  function setDamageCallback(Action1<unit> value)
    value.acquire()
    if (m_damageCallback != null)
      m_damageCallback.release()
    m_damageCallback = value

// ============================================================================
public class MissileAreaDamageSystem extends ECSystem2<ECMissile, ECMissileAreaDamage>

  // --------------------------------------------------------------------------
  construct()
    super(ECMissile.typeId, ECMissileAreaDamage.typeId)

  // --------------------------------------------------------------------------
  override protected function updateEntity(ECEntity entity, ECMissile missile, ECMissileAreaDamage missileAreaDamage, real dt)

    if (entity.isMarkedForDestroy())
      return

    if (not missileAreaDamage.m_initialized)
      missileAreaDamage.m_lastPos = lerp(missile.m_startPos, missile.getTargetPos(), missile.m_progress)
      missileAreaDamage.m_lastSize = lerp(missileAreaDamage.m_startSize, missileAreaDamage.m_endSize, missile.m_progress)
      missileAreaDamage.m_initialized = true

    let pos1 = missileAreaDamage.m_lastPos
    let pos2 = lerp(missile.m_startPos, missile.getTargetPos(), missile.m_progress)

    let v = pos2 - pos1
    let dist = pos1.distanceTo(pos2)
    let vdir = v.norm()
    
    let size1 = missileAreaDamage.m_lastSize
    let size2 = lerp(missileAreaDamage.m_startSize, missileAreaDamage.m_endSize, missile.m_progress)

    let center = pos1 + vdir * (dist + (size2 - size1)) * 0.5
    let radius = (dist + size1 + size2) * 0.5

    // let fx1 = addEffect("circle.mdl", center)..setScale(radius / 64.0)..setColor(0, 255, 0)
    // let fx2 = addEffect("axis.mdl", center)
    // let fx3 = addEffect("circle.mdl", pos1)..setScale(size1 / 64.0)..setColor(0, 0, (missile.m_progress * 255).floor())
    // let fx4 = addEffect("circle.mdl", pos2)..setScale(size2 / 64.0)..setColor(0, 0, (missile.m_progress * 255).floor())
    // doAfter(3.0) () ->
    //   fx1.destr()
    //   fx2.destr()
    //   fx3.destr()
    //   fx4.destr()

    let damageCallback = missileAreaDamage.getDamageCallback()
    let targetFilter = missileAreaDamage.getTargetFilter()

    g_tempGroup.enumUnitsInRange(center, radius)

    for u in g_tempGroup
    //{
      if ((u != missile.m_owner or not missileAreaDamage.m_ignoreMissileOwner)
          and (u != missile.m_targetUnit or not missileAreaDamage.m_ignoreMissileTarget)
          and not missileAreaDamage.m_affectedUnits.contains(u)
          and (targetFilter == null or targetFilter.call(u)))
      //{
        
        let p = u.getPos()
        let pp = pos1 + (p - pos1).project(vdir)
        let pt = parameterize01(pos1, pos2, pp)
        let r = lerp(size1, size2, pt)

        if (pp.distanceToSq(p) < r*r)
        //{
          missileAreaDamage.m_affectedUnits.addUnit(u)
          damageCallback.call(u)
          // u.setVertexColor(color(0,255,0))
        //}
        // else
        //   u.setVertexColor(color(255,0,0))
      //}
    //}

    missileAreaDamage.m_lastPos = pos2
    missileAreaDamage.m_lastSize = size2

// ============================================================================
init

  ECS.registerComponentType(ECMissileEffect.typeId)
    ..registerComponentFactory(entity -> new ECMissileEffect(entity))
    ..registerComponentGetter(entity -> ECMissileEffect.g_components[entity.getId()])

  ECS.registerComponentType(ECMissile.typeId)
    ..registerComponentFactory(entity -> new ECMissile(entity))
    ..registerComponentGetter(entity -> ECMissile.g_components[entity.getId()])

  ECS.registerComponentType(ECMissileAreaDamage.typeId)
    ..registerComponentFactory(entity -> new ECMissileAreaDamage(entity))
    ..registerComponentGetter(entity -> ECMissileAreaDamage.g_components[entity.getId()])

  ECS.registerChannel('MSLE', RealtimeUpdatePriority.Realtime)
  ECS.registerChannel('MSAD', RealtimeUpdatePriority.Slow)

  ECS.registerSystem(new MissileMovementSystem())
    ..withUpdateChannel('MSLE')

  ECS.registerSystem(new MissileEffectSystem())
    ..withUpdateChannel('MSLE')

  ECS.registerSystem(new MissileAreaDamageSystem())
    ..withUpdateChannel('MSAD')