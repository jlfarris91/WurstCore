package TooltipBuilder
import LinkedList
import ErrorHandling
import StringBuilder
import StringExtensions
import ColorUtility
import Func

constant int MAX_TOOLTIP_ITEM_COUNT = 32
constant colorA ITEM_NAME_COLOR = fromHexString("FF3B97D3")
constant colorA ITEM_VALUE_COLOR = COLOR_GOLD

tuple tooltipItemValue(string valueStr, Func1<int, string> valueGetter)

// ============================================================================
public class TooltipItem
  private string m_name
  private tooltipItemValue array[MAX_TOOLTIP_ITEM_COUNT] m_values
  private int m_valueCount

  // --------------------------------------------------------------------------
  construct(string name)
    m_name = name

  // --------------------------------------------------------------------------
  construct(string name, string value)
    m_name = name
    addValue(value)

  // --------------------------------------------------------------------------
  construct(string name, Func1<int, string> getter)
    m_name = name
    addValue(getter)

  // --------------------------------------------------------------------------
  ondestroy
    clear()

  // --------------------------------------------------------------------------
  function clear()
    m_name = null
    for i = 0 to m_valueCount-1
      m_values[i].valueStr = null
      if (m_values[i].valueGetter != null and m_values[i].valueGetter.getDestroyAfterUse())
        destroy m_values[i].valueGetter
      m_values[i].valueGetter = null
    m_valueCount = 0

  // --------------------------------------------------------------------------
  function getName() returns string
    return m_name

  // --------------------------------------------------------------------------
  function getValue(int index) returns string
    return getValue(index, 1)

  // --------------------------------------------------------------------------
  function getValue(int index, int level) returns string
    if (index < 0 or index > m_valueCount - 1)
      return null
    if (m_values[index].valueGetter != null)
      return m_values[index].valueGetter.call(level)
    return m_values[index].valueStr
  
  // --------------------------------------------------------------------------
  function getValueCount() returns int
    return m_valueCount

  // --------------------------------------------------------------------------
  function addValue(string value)
    addValueOrValueGetter(value, null)      

  // --------------------------------------------------------------------------
  function addValue(Func1<int, string> getter)
    addValueOrValueGetter(null, getter)

  // --------------------------------------------------------------------------
  private function addValueOrValueGetter(string valueStr, Func1<int, string> valueGetter)
    if (m_valueCount == MAX_TOOLTIP_ITEM_COUNT)
      error("Reached limit of number of values " + I2S(MAX_TOOLTIP_ITEM_COUNT))
    m_values[m_valueCount].valueStr = valueStr
    m_values[m_valueCount].valueGetter = valueGetter
    m_valueCount++

// ============================================================================
public class TooltipBuilder
  private string m_description
  private LinkedList<TooltipItem> m_items

  // --------------------------------------------------------------------------
  construct()
    m_items = new LinkedList<TooltipItem>()

  // --------------------------------------------------------------------------
  ondestroy 
    clear()

    destroy m_items
    m_items = null

  // --------------------------------------------------------------------------
  function clear()
    m_description = null
    for itm in m_items
      destroy itm
    m_items.clear()

  // --------------------------------------------------------------------------
  function addItem(string name) returns TooltipBuilder
    return addItem(new TooltipItem(name))

  // --------------------------------------------------------------------------
  function addItem(string name, string value) returns TooltipBuilder
    return addItem(new TooltipItem(name, value))

  // --------------------------------------------------------------------------
  function addItem(TooltipItem itm) returns TooltipBuilder
    if (m_items.size() == MAX_TOOLTIP_ITEM_COUNT)
      error("Reached limit of tooltip items " + I2S(MAX_TOOLTIP_ITEM_COUNT))
    m_items.add(itm)
    return this

  // --------------------------------------------------------------------------
  function getDescription() returns string
    return m_description

  // --------------------------------------------------------------------------
  function setDescription(string description) returns TooltipBuilder
    m_description = description
    return this
    
  // --------------------------------------------------------------------------  
  function getTooltipBasic() returns string
    return m_description

  // --------------------------------------------------------------------------  
  function getTooltipExtended() returns string
    let sb = new StringBuilder()

    let padStr = " "
    var index = 0
    for itm in m_items
    //{

      if (index > 0)
        sb.appendLine()

      sb.append(itm.getName().colorize(ITEM_NAME_COLOR))

      let itemValueCount = itm.getValueCount()
      if (itemValueCount == 1)
        sb.append(itm.getValue(0).colorize(ITEM_VALUE_COLOR))
      else
        for j = 0 to itemValueCount - 1
          sb.appendLine()
          sb.append("- " + itm.getValue(j).colorize(ITEM_VALUE_COLOR).padLeft(10, padStr))

      index++
    //}

    return sb.toStringAndDestroy()