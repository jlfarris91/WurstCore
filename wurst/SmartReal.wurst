package SmartReal
import LinkedList
import TimerUtils
import ClosureTimers
import Events
import Dispatcher

public enum AffectorType
  BASE
  SCALE
  ABSOLUTE

// ============================================================================
class SmartRealAffector
  AffectorType m_type
  real m_value
  CallbackManual m_callback = null

  // --------------------------------------------------------------------------
  construct (SmartReal owner, AffectorType _type, real value, real duration)
    m_type = _type
    m_value = value
    if (duration > 0.0)
      m_callback = getTimer().doManual(duration, true, () -> owner.removeAffector(_handle(this castTo int)))

  // --------------------------------------------------------------------------
  ondestroy
    if (m_callback != null)
      destroy m_callback

// ============================================================================
public class SmartReal 
  private real m_base
  private LinkedList<SmartRealAffector> m_affectors
  private Event m_valueChangedEvent

  // --------------------------------------------------------------------------
  construct ()
    m_base = 0.0

  // --------------------------------------------------------------------------
  construct (real initialBaseValue)
    m_base = initialBaseValue

  // --------------------------------------------------------------------------
  ondestroy
    if (m_affectors != null)
      while (m_affectors.isEmpty() == false)
        destroy m_affectors.pop()
      destroy m_affectors
      m_affectors = null
    
    if (m_valueChangedEvent != null)
      destroy m_valueChangedEvent
      m_valueChangedEvent = null

  // --------------------------------------------------------------------------
  function getValue() returns real

    if (m_affectors == null or m_affectors.isEmpty())
      return m_base

    var value = m_base
    var scale = 1.0
    var absolute = 0.0

    for affector in m_affectors
      switch (affector.m_type)
        case BASE
          value = affector.m_value
        case SCALE
          scale *= affector.m_value
        case ABSOLUTE
          absolute += affector.m_value
        
    return value * scale + absolute

  // --------------------------------------------------------------------------
  function setBase(real value)
    if (m_base != value)
      m_base = value
      raiseValueChangedEvent()

  // --------------------------------------------------------------------------
  function setBase(real value, real duration) returns _handle
    return addAffector(AffectorType.BASE, value, duration)

  // --------------------------------------------------------------------------
  function scale(real value) returns _handle
    return addAffector(AffectorType.SCALE, value)

  // --------------------------------------------------------------------------
  function scale(real value, real duration) returns _handle
    return addAffector(AffectorType.SCALE, value, duration)

  // --------------------------------------------------------------------------
  function add(real value) returns _handle
    return addAffector(AffectorType.ABSOLUTE, value)

  // --------------------------------------------------------------------------
  function add(real value, real duration) returns _handle
    return addAffector(AffectorType.ABSOLUTE, value, duration)

  // --------------------------------------------------------------------------
  function subtract(real value) returns _handle
    return addAffector(AffectorType.ABSOLUTE, -value)

  // --------------------------------------------------------------------------
  function subtract(real value, real duration) returns _handle
    return addAffector(AffectorType.ABSOLUTE, -value, duration)

  // --------------------------------------------------------------------------
  function attach(IDispatcher dispatcher) returns SmartRealValue
    return new SmartRealValue(dispatcher, this)

  // --------------------------------------------------------------------------
  function onValueChanged() returns IEvent
    if (m_valueChangedEvent == null)
      m_valueChangedEvent = new Event()
    return m_valueChangedEvent

  // --------------------------------------------------------------------------
  private function raiseValueChangedEvent()
    if (m_valueChangedEvent != null)
      m_valueChangedEvent.call()

  // --------------------------------------------------------------------------
  private function addAffector(AffectorType _type, real value) returns _handle
    return addAffector(_type, value, 0.0)
  
  // --------------------------------------------------------------------------
  private function addAffector(AffectorType _type, real value, real duration) returns _handle
    let affector = new SmartRealAffector(this, _type, value, duration)
    let h = _handle(affector castTo int)
    if (m_affectors == null)
      m_affectors = new LinkedList<SmartRealAffector>()
    m_affectors.add(affector)
    raiseValueChangedEvent()
    return h

  // --------------------------------------------------------------------------
  function removeAffector(_handle h) returns bool
    if (m_affectors == null)
      return false
    let iterator = m_affectors.iterator()
    while iterator.hasNext()
      let affector = iterator.current
      if ((affector castTo int) == h.id)
        iterator.remove()
        destroy affector
        raiseValueChangedEvent()
        return true
    return false
    
// ============================================================================
public class SmartRealValue
  private real m_value = 0.0
  private SmartReal m_source = null
  private _handle m_onValueChangedCallbackHandle = INVALID_HANDLE

  // --------------------------------------------------------------------------
  construct(IDispatcher dispatcher, SmartReal source)
    m_source = source
    m_value = m_source.getValue()
    m_onValueChangedCallbackHandle = source.onValueChanged().registerDispatched(dispatcher) () ->
      m_value = m_source.getValue()

  // --------------------------------------------------------------------------
  ondestroy
    m_source.onValueChanged().unregister(m_onValueChangedCallbackHandle)
    m_onValueChangedCallbackHandle = INVALID_HANDLE

  // --------------------------------------------------------------------------
  function getValue() returns real
    return m_value