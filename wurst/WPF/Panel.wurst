package Panel
import Frame
import Layout

// ============================================================================
public class Panel extends Frame
  private IFrameLayout m_layout

  // --------------------------------------------------------------------------
  construct()
    super(createFrame("FRAME", "", GAME_UI, "", 0))

  // --------------------------------------------------------------------------
  construct(string name)
    super(createFrame("FRAME", name, GAME_UI, "", 0))

  // --------------------------------------------------------------------------
  construct(framehandle frameHandle)
    super(frameHandle)

  // --------------------------------------------------------------------------
  ondestroy
    if (m_layout != null)
      destroy m_layout
      m_layout = null
  
  // --------------------------------------------------------------------------
  override function isPanel() returns bool
    return true

  // --------------------------------------------------------------------------
  function getFrameLayout() returns IFrameLayout
    return m_layout

  // --------------------------------------------------------------------------
  protected function setFrameLayout(IFrameLayout value)
    m_layout = value

  // --------------------------------------------------------------------------
  override function measureOverride(vec2 availableSize) returns vec2
    return m_layout != null
           ? m_layout.measure(this, availableSize)
           : super.measureOverride(availableSize)

  // --------------------------------------------------------------------------
  override function arrange()
    super.arrange()

  // --------------------------------------------------------------------------
  override function arrange(vec2 availableSize)
    super.arrange(availableSize)

  // --------------------------------------------------------------------------
  override function arrangeOverride(vec2 layoutSize) returns vec2
    let paddedLayoutSize = layoutSize - getPadding().size()
    vec2 result
    if (m_layout != null)
      result = m_layout.arrange(this, paddedLayoutSize)
      getFrameHandle().setSize(result)
    else
      super.arrangeOverride(paddedLayoutSize)
      result = paddedLayoutSize
    return result