package Panel
import Frame
import Layout

// ============================================================================
public class Panel extends Frame
  private IFrameLayout m_layout
  private bool m_layingOut

  // --------------------------------------------------------------------------
  construct()
    super(createFrame("FRAME", "", GAME_UI, "", 0))

  // --------------------------------------------------------------------------
  construct(string name)
    super(createFrame("FRAME", name, GAME_UI, "", 0))

  // --------------------------------------------------------------------------
  construct(framehandle frameHandle)
    super(frameHandle)

  // --------------------------------------------------------------------------
  ondestroy
    if (m_layout != null)
      destroy m_layout
      m_layout = null
  
  // --------------------------------------------------------------------------
  override function isPanel() returns bool
    return true

  // --------------------------------------------------------------------------
  function getFrameLayout() returns IFrameLayout
    return m_layout

  // --------------------------------------------------------------------------
  protected function setFrameLayout(IFrameLayout value)
    m_layout = value

  // --------------------------------------------------------------------------
  override function measureOverride(vec2 availableSize) returns vec2
    return m_layout != null
           ? m_layout.measure(this, availableSize)
           : super.measureOverride(availableSize)

  // --------------------------------------------------------------------------
  override function layout()
    if (m_layingOut)
      return
    m_layingOut = true
    super.layout()
    m_layingOut = false

  // --------------------------------------------------------------------------
  override function layout(vec2 availableSize)
    super.layout(availableSize)

  // --------------------------------------------------------------------------
  override function layoutOverride(vec2 layoutSize) returns vec2
    let paddedLayoutSize = layoutSize - getPadding().size()
    vec2 result
    if (m_layout != null)
      result = m_layout.layout(this, paddedLayoutSize)
      getFrameHandle().setSize(result)
    else
      super.layoutOverride(paddedLayoutSize)
      result = paddedLayoutSize
    return result