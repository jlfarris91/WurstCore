package GroupExtensions
import Range

group g_workingGroup = CreateGroup()
unit array g_scratchpad

// ============================================================================
public function group.getFirst() returns unit
  return FirstOfGroup(this)

// ============================================================================
public function group.getClosestUnit(vec2 pos) returns unit
  return this.getClosestUnit(pos, REAL_MAX)

// ============================================================================
public function group.getClosestUnit(vec2 pos, real maxDist) returns unit
  return this.getClosestUnit(pos, rangeReal(0, maxDist))

// ============================================================================
public function group.getClosestUnit(vec2 pos, rangeReal range) returns unit
  let minRangeSq = range.min * range.min
  var closestDistSq = range.max * range.max
  unit closestUnit = null
  for _unit in this
    let distSq = _unit.getPos().distanceToSq(pos)
    if (distSq >= minRangeSq and distSq <= closestDistSq)
      closestDistSq = distSq
      closestUnit = _unit
  return closestUnit

// ============================================================================
/** Do not call recursively */
public function group.getRandomUnitInRange(vec2 pos, rangeReal range) returns unit
  let minRangeSq = range.min * range.min
  let maxRangeSq = range.max * range.max
  var scratchpadCount = 0
  for _unit in this
    let distSq = _unit.getPos().distanceToSq(pos)
    if (distSq >= minRangeSq and distSq <= maxRangeSq)
      g_scratchpad[scratchpadCount] = _unit
      scratchpadCount++
  if (scratchpadCount == 0)
    return null
  let selectedUnit = g_scratchpad[GetRandomInt(0, scratchpadCount-1)]
  for i = 0 to scratchpadCount-1
    g_scratchpad[i] = null
  return selectedUnit

// ============================================================================
/** Removes any "ghost" units that have been removed from the game but not from the group
    https://www.hiveworkshop.com/threads/removing-phantom-units-from-a-unit-group.327042/
*/
public function group.refresh()
  g_workingGroup.clear()
  g_workingGroup.addGroup(this)
  this.clear()
  this.addGroup(g_workingGroup)
  g_workingGroup.clear()
  