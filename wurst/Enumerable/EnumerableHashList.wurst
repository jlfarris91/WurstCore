package EnumerableHashList
import initlater Enumerable
import HashList

// ============================================================================
/** Returns the HashList<T> typed as IEnumerable<T>. */
public function HashList<T>.asEnumerable<T>() returns IEnumerable<T>
  return new HashListEnumerableAdapter(this, false)

// ============================================================================
/** Returns the HashList<T> typed as IEnumerable<T>. */
public function HashList<T>.asEnumerable<T>(bool owner) returns IEnumerable<T>
  return new HashListEnumerableAdapter(this, owner)

// ============================================================================
public function IEnumerable<TSource>.toHashList<TSource>() returns HashList<TSource>
  let iter = this.iterator()
  let result = new HashList<TSource>()
  while iter.hasNext()
    result.add(iter.next())
  destroy iter
  return result

// ============================================================================
class HashListEnumerableAdapter<T> implements IEnumerable<T>
  private HashList<T> _list
  private bool _ownsList

  // --------------------------------------------------------------------------
  construct(HashList<T> list, bool ownsList)
    _list = list
    _ownsList = ownsList

  ondestroy
    if (_ownsList)
      destroy _list
    _list = null

  // --------------------------------------------------------------------------
  override function iterator() returns IEnumerator<T>
    return new HashListEnumeratorAdapter<T>(_list.iterator())

// ============================================================================
class HashListEnumeratorAdapter<T> implements IEnumerator<T>
  private HLIterator<T> _iterator

  // --------------------------------------------------------------------------
  construct(HLIterator<T> iterator)
    _iterator = iterator

  // --------------------------------------------------------------------------
  ondestroy
    destroy _iterator
    _iterator = null

  // --------------------------------------------------------------------------
  override function hasNext() returns bool
    return _iterator.hasNext()

  // --------------------------------------------------------------------------
  override function next() returns T
    return _iterator.next()

  // --------------------------------------------------------------------------
  override function close()
    destroy this