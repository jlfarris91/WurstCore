package UnitMetadata
import Composition
import LinkedList
import public UnitMetadataExtensions
import ErrorHandling
import Type
import ErrorIf
import ClosureEvents
import initlater UnitComponent
import UnitExtensions
import Events
import UnitIndexer

IUnitMetadata array g_unitMetadatas
LinkedList<IUnitMetadataFactory> g_unitMetadataFactories
int g_ignoreIndexingCounter

// ============================================================================
public interface IUnitMetadata extends IComposite
  function getUnit() returns unit
  function attach(unit u)
  function detach()

// ============================================================================
public interface IUnitMetadataFactory
  function createUnit(unit u) returns IUnitMetadata

// ============================================================================
public class UnitMetadata implements IUnitMetadata
  protected unit m_unit
  private CompositionManager m_compositionManager
  private AnonymousEventHandler m_unitDamagedEventHandler

  // --------------------------------------------------------------------------
  ondestroy
    if (m_compositionManager != null)
      destroy m_compositionManager
      m_compositionManager = null

    unlistenToUnitDamagedEvent()

    m_unit = null

  // --------------------------------------------------------------------------
  @inline
  override function getUnit() returns unit
    return m_unit

  // --------------------------------------------------------------------------
  override function attach(unit unitToAttachTo)
    ErrorIf.argumentIsNull(unitToAttachTo, "unitToAttachTo")

    if (unitToAttachTo == m_unit)
      return
  
    // This unit was not indexed for whatever reason
    let newIndex = unitToAttachTo.getIndex()
    if (newIndex == 0)
      error("Attempting to attach to a deindexed unit")
      return

    // The new unit was indexed so we should assign the metadata
    g_unitMetadatas[newIndex] = this
    setUnit(unitToAttachTo)

    Log.debug("[UnitMetadata.attach] Attached to " + this.getName())

  // --------------------------------------------------------------------------
  override function detach()

    let currentUnit = getUnit()
    if (currentUnit == null)
      error("Metadata has no assigned unit")
      return

    let currentIndex = currentUnit.getIndex()
    if (currentIndex == 0)
      error("Metadata was assigned an unindexed unit")
      return

    g_unitMetadatas[currentIndex] = null
    setUnit(null)

    Log.debug("[UnitMetadata.detach] Detached from " + this.getName())

  // --------------------------------------------------------------------------
  function onKilled()
    setAllComponentsEnabled(false)

  // --------------------------------------------------------------------------
  protected function onUnitChanged(unit oldUnit, unit newUnit)
    if (m_compositionManager != null)
      for comp in m_compositionManager
        let unitComp = comp castTo UnitComponent
        if (unitComp != null)
          unitComp.onUnitChanged(oldUnit, newUnit)

  // --------------------------------------------------------------------------
  private function setUnit(unit u)
    let oldUnit = m_unit
    let newUnit = u
    if (oldUnit != newUnit)
      m_unit = newUnit
      onUnitChanged(oldUnit, newUnit)

  // --------------------------------------------------------------------------
  @inline
  override function addComponent(IComponent component) returns IComponent
    if (m_compositionManager == null)
      m_compositionManager = new CompositionManager()
    return m_compositionManager.addComponent(component)

  // --------------------------------------------------------------------------
  @inline
  override function removeComponent(IComponent component) returns bool
    if (m_compositionManager == null)
      return false
    return m_compositionManager.removeComponent(component)

  // --------------------------------------------------------------------------
  @inline
  override function getComponent(Type componentType) returns IComponent
    return m_compositionManager != null ? m_compositionManager.getComponent(componentType) : null

  // --------------------------------------------------------------------------
  protected function onDamaged()
    skip

  // --------------------------------------------------------------------------
  protected function listenToUnitDamagedEvent()
    if (m_unitDamagedEventHandler != null)
      return
    m_unitDamagedEventHandler= PlayerUnitEvents.unitDamaged.addListener() -> 
      if (GetEventDamageSource() == getUnit())
        onDamaged()

  // --------------------------------------------------------------------------
  protected function unlistenToUnitDamagedEvent()
    if (m_unitDamagedEventHandler == null)
      return
    PlayerUnitEvents.unitDamaged.removeListener(m_unitDamagedEventHandler)
    m_unitDamagedEventHandler = null

  // --------------------------------------------------------------------------
  private function setAllComponentsEnabled(bool enabled)
    if (m_compositionManager != null)
      for comp in m_compositionManager
        comp.setEnabled(enabled)

// ============================================================================
public function unit.hasMetadata() returns bool
  return this.getMetadata() != null

// ============================================================================
public function unit.getMetadata() returns IUnitMetadata
  let index = this.getIndex()
  if (index == 0)
    return null
  return g_unitMetadatas[index]

// ============================================================================
public function unit.getMetadataRequired() returns IUnitMetadata
  let index = this.getIndex()
  if (index == 0)
    error("No metadata found for unit " + this.getName())
  return g_unitMetadatas[index]

// ============================================================================
public function registerUnitMetadataFactory(IUnitMetadataFactory factory)
  ErrorIf.argumentIsNull(factory, "factory")
  g_unitMetadataFactories.addAt(factory, 0)

// ============================================================================
public function unregisterUnitMetadataFactory(IUnitMetadataFactory factory)
  ErrorIf.argumentIsNull(factory, "factory")
  g_unitMetadataFactories.remove(factory)

// ============================================================================
public function constructUnitMetadata(unit u) returns IUnitMetadata
  ErrorIf.argumentIsNull(u, "u")
  IUnitMetadata metadata = null
  for factory in g_unitMetadataFactories
    let uu = factory.createUnit(u)
    if (uu != null)
      metadata = uu
      break
  if (metadata == null)
    metadata = new UnitMetadata()
  metadata.attach(u)
  return metadata

// ============================================================================
public function createUnitTLS(player owner, integer unitId, real x, real y, real face) returns unit
  return CreateUnit(owner, unitId, x, y, face)

// ============================================================================
public function createUnitTLS(IUnitMetadata metadata, player owner, integer unitId, real x, real y, real face) returns unit
  ErrorIf.argumentIsNull(metadata, "metadata")

  let oldUnit = metadata.getUnit()
  metadata.detach()

  startIgnoreIndexing()
  let newUnit = CreateUnit(owner, unitId, x, y, face)
  stopIgnoreIndexing()

  if (newUnit.hasMetadata())
    error("[createUnitTLS] Unit should not have metadata yet")
    return null

  metadata.attach(newUnit)

  Log.debug("[createUnitTLS] Reattached metadata from unit " + (oldUnit != null ? oldUnit.getName() : "null") + " to unit " + newUnit.getName())

  return newUnit

// ============================================================================
public function replaceUnitTLS(unit whichUnit, integer newUnitId, integer unitStateMethod) returns unit
  ErrorIf.argumentIsNull(whichUnit, "whichUnit")

  let oldUnitName = whichUnit.getName()
  let existingMetadata = whichUnit.getMetadata()

  if (existingMetadata != null)
    existingMetadata.detach()
    startIgnoreIndexing()
  else
    Log.debug("[replaceUnitTLS] Replacing unit " + whichUnit.getName() + " with no attached metadata")

  let newUnit = replaceUnitFixedTLS(whichUnit, newUnitId, unitStateMethod)

  if (newUnit.hasMetadata())
    error("Unit should not have metadata yet")
  
  if (existingMetadata != null)
    stopIgnoreIndexing()
    existingMetadata.attach(newUnit)

  Log.debug("[replaceUnitTLS] Reattached metadata from unit " + oldUnitName + " to unit " + newUnit.getName())
    
  return newUnit

// ============================================================================
function onUnitKilled()
  let dyingUnit = GetDyingUnit()
  
  let metadata = dyingUnit.getMetadata() castTo UnitMetadata
  if (metadata == null)
    return

  metadata.onKilled()
  Log.debug("[onUnitKilled] " + dyingUnit.getName() + " was killed")

// ============================================================================
function onUnitIndexed(unit indexingUnit)
  if (isIgnoringIndexing())
    Log.debug("[onUnitIndexed] Ignoring indexed unit")
    return

  let index = indexingUnit.getIndex()
  
  var metadata = g_unitMetadatas[index]
  if (metadata != null)
    error("Metadata already exists for unit " + indexingUnit.getName())
    return

  metadata = constructUnitMetadata(indexingUnit)
  g_unitMetadatas[index] = metadata

  Log.debug("[onUnitIndexed] Created metadata for unit " + indexingUnit.getName())

// ============================================================================
function onUnitDeindexed(unit indexingUnit)
  if (isIgnoringIndexing())
    Log.debug("[onUnitDeindexed] Ignoring deindexed unit")
    return

  let index = indexingUnit.getIndex()
  let metadata = indexingUnit.getMetadata()

  if (metadata != null)
    destroy metadata
    Log.debug("[onUnitDeindexed] Destroyed metadata for unit " + indexingUnit.getName())

  g_unitMetadatas[index] = null

// ============================================================================
public function startIgnoreIndexing()
  g_ignoreIndexingCounter++
  Log.debug("Start ignoring indexing " + I2S(g_ignoreIndexingCounter))

// ============================================================================
public function stopIgnoreIndexing()
  g_ignoreIndexingCounter--
  Log.debug("Stop ignoring indexing " + I2S(g_ignoreIndexingCounter))

// ============================================================================
public function isIgnoringIndexing() returns bool
  return g_ignoreIndexingCounter > 0

// ============================================================================
init
  g_unitMetadataFactories = new LinkedList<IUnitMetadataFactory>()
  g_ignoreIndexingCounter = 0

  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitKilled)

  onUnitIndex(() -> onUnitIndexed(getIndexingUnit()))
  onUnitDeindex(() -> onUnitDeindexed(getIndexingUnit()))
