package UnitComponent
import public UnitMetadata
import public initlater Component
import ClosureTimers
import ErrorHandling
import Object
import Events
import Action

// ============================================================================
public class UnitComponentCreationContext
  IUnitMetadata owner

// ============================================================================
public constant int typeId_UnitComponent = Meta.reserveAbstractClass("UnitComponent.UnitComponent")
public abstract class UnitComponent extends Component
  private IUnitMetadata m_owner
  private bool m_disableOnDeath = true

  // --------------------------------------------------------------------------
  construct(IUnitMetadata ownerUnit)
    super(ownerUnit)
    m_owner = ownerUnit

  // --------------------------------------------------------------------------
  override function getOwner() returns IUnitMetadata
    return m_owner

  // --------------------------------------------------------------------------
  function getOwnerUnit() returns unit
    return m_owner.getUnit()

  // --------------------------------------------------------------------------
  function getOwningPlayer() returns player
    return getOwnerUnit().getOwner()

  // --------------------------------------------------------------------------
  function setDisableOnDeath(bool disableOnDeath)
    m_disableOnDeath = disableOnDeath

  // --------------------------------------------------------------------------
  function getDisableOnDeath() returns bool
    return m_disableOnDeath

  // --------------------------------------------------------------------------
  function onOwnerKilled()
    if (m_disableOnDeath)
      disable()

  // --------------------------------------------------------------------------
  function onUnitChanged(unit _oldUnit, unit _newUnit)
    skip

  // --------------------------------------------------------------------------
  protected function dispatch(Action work)
    dispatch(true, work)

  // --------------------------------------------------------------------------
  protected function dispatch(bool requireEnabled, Action work)
    dispatch(getOwnerUnit(), this.typeId, requireEnabled, work)

  // --------------------------------------------------------------------------
  protected static function dispatch(unit ownerUnit, int compTypeId, bool requireEnabled, Action work)
    work.acquire()
    if (ownerUnit != null and ownerUnit.isAlive())
      let comp = ownerUnit.getComponent(compTypeId)
      if (comp != null)
        if (not requireEnabled or comp.getEnabled())
          work.call()
    /*
        else
          Log.debug("UnitComponent", "dispatch", ownerUnit.getName(), "Component must be enabled")
      else
        Log.debug("UnitComponent", "dispatch", ownerUnit.getName(), "Unit has no component of type " + Meta.typeIdToTypeName(compTypeId))
    else
      Log.debug("UnitComponent", "dispatch", ownerUnit.getName(), "Unit is dead")
    */
    work.release()

  // --------------------------------------------------------------------------
  protected function dispatch(real seconds, Action work) returns CallbackSingle
    return dispatch(seconds, true, work)

  // --------------------------------------------------------------------------
  protected function dispatch(real seconds, bool requireEnabled, Action work) returns CallbackSingle
    return dispatch(getOwnerUnit(), this.typeId, seconds, requireEnabled, work)

  // --------------------------------------------------------------------------
  protected static function dispatch(unit ownerUnit, int compTypeId, real seconds, bool requireEnabled, Action work) returns CallbackSingle
    if (ownerUnit == null)
      argumentNullError("ownerUnit")
    if (work == null)
      argumentNullError("work")
    return doAfter(seconds, new DispatchedCallbackSingle(ownerUnit, compTypeId, requireEnabled, work))

// ============================================================================
public function Event.registerDispatched(UnitComponent comp, Action handler) returns _handle
  return this.registerDispatched(comp, true, handler)

// ============================================================================
public function Event.registerDispatched(UnitComponent comp, bool requireEnabled, Action handler) returns _handle
  return this.register(new DispatchedEventCallback(comp.getOwnerUnit(), comp.typeId, requireEnabled, handler))

// ============================================================================
module DispatchedWorkModule
  protected unit m_ownerUnit
  protected int m_compTypeId
  protected bool m_requireEnabled
  protected Action m_work

  // --------------------------------------------------------------------------
  function _construct(unit ownerUnit, int compTypeId, bool requireEnabled, Action work)
    if (ownerUnit == null)
      argumentNullError("ownerUnit")
    if (work == null)
      argumentNullError("work")
    m_ownerUnit = ownerUnit
    m_compTypeId = compTypeId
    m_requireEnabled = requireEnabled
    m_work = work.acquire()
  
  // --------------------------------------------------------------------------
  ondestroy
    if (m_work != null)
      m_work.release()
    m_work = null

// ============================================================================
class DispatchedCallbackSingle extends CallbackSingle
  use DispatchedWorkModule

  // --------------------------------------------------------------------------
  construct(unit ownerUnit, int compTypeId, bool requireEnabled, Action work)
    _construct(ownerUnit, compTypeId, requireEnabled, work)

  // --------------------------------------------------------------------------
  override function call()
    UnitComponent.dispatch(m_ownerUnit, m_compTypeId, m_requireEnabled, m_work)

// ============================================================================
class DispatchedEventCallback extends EventCallback
  use DispatchedWorkModule

  // --------------------------------------------------------------------------
  construct(unit ownerUnit, int compTypeId, bool requireEnabled, Action work)
    _construct(ownerUnit, compTypeId, requireEnabled, work)

  // --------------------------------------------------------------------------
  override function call()
    UnitComponent.dispatch(m_ownerUnit, m_compTypeId, m_requireEnabled, m_work)

// ============================================================================
init
  Meta.register("UnitComponent", (registrar) -> registerMetadata(registrar))

///////////////////////////////////////////////////////////////////////////////
function registerMetadata(MetaRegistrar registrar)
  registrar.registerAbstractClass(typeId_UnitComponent, "UnitComponent.UnitComponent")..setBase(typeId_Component)
  ..registerDestructor((object object) -> destroy object.cast<UnitComponent>(typeId_Component))

  registrar.registerClass(UnitComponentCreationContext.typeId)
///////////////////////////////////////////////////////////////////////////////