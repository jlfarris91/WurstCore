package Grid
import Bounds
import Action
import Math
import ErrorHandling

int INVALID_INDEX = -1

public tuple cell(int row, int column)
public tuple span(cell min, cell max)

public constant cell INVALID_CELL = cell(INVALID_INDEX, INVALID_INDEX)

// ============================================================================
public interface IGrid
  // --------------------------------------------------------------------------
  function getBounds() returns bounds

  // --------------------------------------------------------------------------
  function getWidth() returns int

  // --------------------------------------------------------------------------
  function getHeight() returns int

  // --------------------------------------------------------------------------
  function getCell(vec2 pos) returns cell

  // --------------------------------------------------------------------------
  function getIndex(cell cell) returns int

  // --------------------------------------------------------------------------
  function getCellBounds(cell cell) returns bounds

// ============================================================================
public class Grid implements IGrid
  private bounds m_bounds
  private int m_width
  private int m_height
  private vec2 m_cellSize

  // --------------------------------------------------------------------------
  construct(bounds bounds, int width, int height)
    m_bounds = bounds
    m_width = max(width, 1)
    m_height = max(height, 1)

    let size = m_bounds.size()
    
    if (size.x == 0.0)
      error("bounds has width of 0")
    if (size.y == 0.0)
      error("bounds has height of 0")

    m_cellSize = vec2(max(size.x / m_width, 1), max(size.y / m_height, 1))

  // --------------------------------------------------------------------------
  override function getBounds() returns bounds
    return m_bounds

  // --------------------------------------------------------------------------
  override function getWidth() returns int
    return m_width

  // --------------------------------------------------------------------------
  override function getHeight() returns int
    return m_height

  // --------------------------------------------------------------------------
  override function getCell(vec2 worldPos) returns cell
    if (not m_bounds.contains(worldPos))
      return INVALID_CELL
    let localPos = m_bounds.toLocalPos(worldPos)
    return getCellFromLocalPos(localPos)

  // --------------------------------------------------------------------------
  function getCell(int index) returns cell
    return cell(index, m_width, m_height)

  // --------------------------------------------------------------------------
  override function getIndex(cell cell) returns int
    return cell.toIndex(m_width, m_height)

  // --------------------------------------------------------------------------
  override function getCellBounds(cell cell) returns bounds
    if (cell.row < 0 or cell.row >= m_height)
      error("Argument out of range: cell.row")
    if (cell.column < 0 or cell.column >= m_width)
      error("Argument out of range: cell.column")
    let min = m_bounds.min + vec2(m_cellSize.x * cell.column, m_cellSize.y * cell.row)
    let max = min + m_cellSize
    return bounds(min, max)
    
  // --------------------------------------------------------------------------
  function forEachCell(Action2<int, int> action)
    action.acquire()
    for r = 0 to m_height - 1
      for c = 0 to m_width - 1
        action.call(r, c)
    action.release()

  // --------------------------------------------------------------------------
  function getArea(bounds bounds) returns span
    return getArea(bounds.min, bounds.max)

  // --------------------------------------------------------------------------
  function getArea(vec2 min, vec2 max) returns span
    let clampedMin = getCell(clamp(min, m_bounds.min, m_bounds.max))
    let clampedMax = getCell(clamp(max, m_bounds.min, m_bounds.max))
    return span(clampedMin, clampedMax)

  // --------------------------------------------------------------------------
  private function getCellFromLocalPos(vec2 localPos) returns cell
    let x = clamp(R2I(localPos.x / m_cellSize.x), 0, m_width - 1)
    let y = clamp(R2I(localPos.y / m_cellSize.y), 0, m_height - 1)    
    return cell(y, x)

// ============================================================================
public function cell.toIndex(int width, int height) returns int
  if (this.column < 0 or this.column >= width or
      this.row < 0 or this.row >= height)
    return INVALID_INDEX
  return this.row * width + this.column

// ============================================================================
public function cell.toString() returns string
  return "(r: {0}, c: {1})".format(I2S(this.row), I2S(this.column))

// ============================================================================
public function cell(int index, int width, int height) returns cell
  if (index < 0 or index >= width*height)
    error("Argument out of range: index")
  return cell(index div width, index mod width)

// ============================================================================
public function span.toString() returns string
  return "(min: {0}, max: {1})".format(this.min.toString(), this.max.toString())

// ============================================================================
public function cell.assertEquals(cell expected)
  if this != expected
    testFail("Expected <" + expected.toString() + ">, Actual <" + this.toString() + ">")

// ============================================================================
@test
function grid_getCell_checkBottomLeft()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act + assert
  grid.forEachCell() (integer r, integer c) ->
    let testPos = vec2(I2R(r), I2R(c)) * 10
    let cell = grid.getCell(testPos)
    cell.assertEquals(cell(r,c))

// ============================================================================
@test
function grid_getCell_checkCenter()
  // arrange
  let cellW = 10
  let cellH = 10
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), cellW, cellH)

  // act + assert
  grid.forEachCell() (integer r, integer c) ->
    let testPos = vec2(I2R(r), I2R(c)) * 10 + vec2(I2R(cellW), I2R(cellH)) / 2
    let cell = grid.getCell(testPos)
    cell.assertEquals(cell(r,c))

// ============================================================================
@test
function grid_getCell_checkTopRightCorner()
  // arrange
  let cellW = 10
  let cellH = 10
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), cellW, cellH)

  // act + assert
  grid.forEachCell() (integer r, integer c) ->
    let testPos = vec2(I2R(r), I2R(c)) * 10 + vec2(I2R(cellW), I2R(cellH))
    let cell = grid.getCell(testPos)
    cell.assertEquals(cell(min(r + 1, cellH - 1), min(c + 1, cellW - 1)))

// ============================================================================
@test
function grid_getCell_checkGridCorners()
  // arrange
  let min = vec2(-100, -100)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act + assert
  grid.getCell(vec2(-100, -100)).assertEquals(cell(0, 0)) // Bottom left corner
  grid.getCell(vec2(99, 99)).assertEquals(cell(9, 9))     // Top right corner
  grid.getCell(vec2(-100, 99)).assertEquals(cell(0, 9))   // Top left corner
  grid.getCell(vec2(99, -100)).assertEquals(cell(9, 0))   // Bottom right corner

  grid.getCell(vec2(-1, -1)).assertEquals(cell(4, 4))    // Almost center
  grid.getCell(vec2(0, 0)).assertEquals(cell(5, 5))      // Exactly center
  grid.getCell(vec2(1, 1)).assertEquals(cell(5, 5))      // A little past center
  grid.getCell(vec2(-1, 1)).assertEquals(cell(4, 5))     // Almost center
  grid.getCell(vec2(1, -1)).assertEquals(cell(5, 4))     // Almost center

// ============================================================================
@test
function grid_getArea_center_rangeLessThanOneCell()
  // arrange
  let min = vec2(-100, -100)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(0, 0), vec2(5, 5))

  // assert
  area.min.assertEquals(cell(4, 4))
  area.max.assertEquals(cell(5, 5))

// ============================================================================@test
function grid_getArea_center_rangeEqualToTwoCells()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(50, 50), vec2(15, 15))

  // assert
  area.min.assertEquals(cell(4, 4))
  area.max.assertEquals(cell(5, 5))

// ============================================================================
@test
function grid_getArea_bottomLeftCorner_shouldReturnOneCell()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(0, 0), vec2(10, 10))

  // assert
  area.min.assertEquals(cell(0, 0))
  area.max.assertEquals(cell(0, 0))

// ============================================================================
@test
function grid_getArea_bottomLeftCorner_shouldReturn4Cells()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(0, 0), vec2(20, 20))

  // assert
  area.min.assertEquals(cell(0, 0))
  area.max.assertEquals(cell(1, 1))

// ============================================================================
@test
function grid_getArea_topRightCorner_shouldReturnOneCell()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(100, 100), vec2(10, 10))

  // assert
  area.min.assertEquals(cell(9, 9))
  area.max.assertEquals(cell(9, 9))

// ============================================================================
@test
function grid_getArea_topRightCorner_shouldReturn4Cells()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(100, 100), vec2(20, 20))

  // assert
  area.min.assertEquals(cell(9, 9))
  area.max.assertEquals(cell(9, 9))

// ============================================================================
@test
function grid_getArea_topRightCorner_shouldReturn4Cells_2()
  // arrange
  let min = vec2(0, 0)
  let max = vec2(100, 100)
  let grid = new Grid(bounds(min, max), 10, 10)

  // act
  let area = grid.getArea(vec2(99, 99), vec2(20, 20))

  // assert
  area.min.assertEquals(cell(8, 8))
  area.max.assertEquals(cell(9, 9))