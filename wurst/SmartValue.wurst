package SmartValue
import LinkedList
import TimerUtils
import ClosureTimers
import Events
import Dispatcher
import ErrorHandling

// ============================================================================
public enum AffectorType
  BASE
  SCALE
  ABSOLUTE

// ============================================================================
class SmartValueAffector
  AffectorType m_type
  real m_value
  CallbackManual m_callback = null

  // --------------------------------------------------------------------------
  construct (SmartValue owner, AffectorType _type, real value, real duration)
    m_type = _type
    m_value = value
    if (duration > 0.0)
      m_callback = getTimer().doManual(duration, true, () -> owner.removeAffector(_handle(this castTo int)))

  // --------------------------------------------------------------------------
  ondestroy
    if (m_callback != null)
      destroy m_callback

// ============================================================================
public abstract class SmartValueBase

// ============================================================================
public class SmartValue
  private real m_base
  protected LinkedList<SmartValueAffector> m_affectors = null
  protected Event m_valueChangedEvent = null
  private SmartValue m_source = null
  private IDispatcher m_onValueChangedCallbackDispatcher = null
  private _handle m_onValueChangedCallbackHandle = INVALID_HANDLE

  // --------------------------------------------------------------------------
  construct ()
    m_base = 0.0

  // --------------------------------------------------------------------------
  construct (real initialBaseValue)
    m_base = initialBaseValue

  // --------------------------------------------------------------------------
  ondestroy
    detach()
    clearAffectors()

    if (m_valueChangedEvent != null)
      destroy m_valueChangedEvent
      m_valueChangedEvent = null

  // --------------------------------------------------------------------------
  function getValue() returns real

    if (m_affectors == null or m_affectors.isEmpty())
      return m_base

    var value = m_base
    var scale = 1.0
    var absolute = 0.0

    for affector in m_affectors
      switch (affector.m_type)
        case BASE
          value = affector.m_value
        case SCALE
          scale *= affector.m_value
        case ABSOLUTE
          absolute += affector.m_value
        
    return value * scale + absolute

  // --------------------------------------------------------------------------
  function getIntValue() returns int
    return getValue().toInt()

  // --------------------------------------------------------------------------
  function setBase(real value)
    if (m_base != value)
      m_base = value
      raiseValueChangedEvent()

  // --------------------------------------------------------------------------
  function setBase(int value)
    setBase(value.toReal())

  // --------------------------------------------------------------------------
  function setBase(real value, real duration) returns _handle
    return addAffector(AffectorType.BASE, value, duration)

  // --------------------------------------------------------------------------
  function scale(real value) returns _handle
    return addAffector(AffectorType.SCALE, value)

  // --------------------------------------------------------------------------
  function scale(real value, real duration) returns _handle
    return addAffector(AffectorType.SCALE, value, duration)

  // --------------------------------------------------------------------------
  function add(real value) returns _handle
    return addAffector(AffectorType.ABSOLUTE, value)

  // --------------------------------------------------------------------------
  function add(real value, real duration) returns _handle
    return addAffector(AffectorType.ABSOLUTE, value, duration)

  // --------------------------------------------------------------------------
  function subtract(real value) returns _handle
    return addAffector(AffectorType.ABSOLUTE, -value)

  // --------------------------------------------------------------------------
  function subtract(real value, real duration) returns _handle
    return addAffector(AffectorType.ABSOLUTE, -value, duration)

  // --------------------------------------------------------------------------
  protected function addAffector(AffectorType _type, real value) returns _handle
    return addAffector(_type, value, 0.0)
  
  // --------------------------------------------------------------------------
  protected function addAffector(AffectorType _type, real value, real duration) returns _handle
    let affector = new SmartValueAffector(this, _type, value, duration)
    let h = _handle(affector castTo int)
    if (m_affectors == null)
      m_affectors = new LinkedList<SmartValueAffector>()
    m_affectors.add(affector)
    raiseValueChangedEvent()
    return h

  // --------------------------------------------------------------------------
  function removeAffector(_handle h) returns bool
    if (m_affectors == null)
      return false
    let iterator = m_affectors.iterator()
    while iterator.hasNext()
      let affector = iterator.current
      if ((affector castTo int) == h.id)
        iterator.remove()
        destroy affector
        raiseValueChangedEvent()
        return true
    return false

  // --------------------------------------------------------------------------
  function clearAffectors()
    if (m_affectors != null)
      while (m_affectors.isEmpty() == false)
        destroy m_affectors.pop()
      destroy m_affectors
      m_affectors = null
    raiseValueChangedEvent()

  // --------------------------------------------------------------------------
  function attach(IDispatcher dispatcher) returns SmartValue
    let smartValue = new SmartValue()
    smartValue.attach(this, dispatcher)
    return smartValue

  // --------------------------------------------------------------------------
  function attach(SmartValue source, IDispatcher dispatcher)
    
    if (m_source == source and m_onValueChangedCallbackDispatcher == dispatcher)
      return
      
    if (source == null)
      argumentNullError("source")

    if (dispatcher == null)
      argumentNullError("dispatcher")

    detach()

    m_source = source
    m_onValueChangedCallbackDispatcher = dispatcher

    attach()

  // --------------------------------------------------------------------------
  private function attach()
    setBase(m_source.getValue())
    m_onValueChangedCallbackHandle = m_source.onValueChanged().registerDispatched(m_onValueChangedCallbackDispatcher) () ->
      setBase(m_source.getValue())

  // --------------------------------------------------------------------------
  function detach()
    if (m_source != null and m_onValueChangedCallbackHandle != INVALID_HANDLE)
      m_source.onValueChanged().unregister(m_onValueChangedCallbackHandle)
      m_onValueChangedCallbackHandle = INVALID_HANDLE

  // --------------------------------------------------------------------------
  function onValueChanged() returns IEvent
    if (m_valueChangedEvent == null)
      m_valueChangedEvent = new Event()
    return m_valueChangedEvent

  // --------------------------------------------------------------------------
  protected function raiseValueChangedEvent()
    if (m_valueChangedEvent != null)
      m_valueChangedEvent.call()